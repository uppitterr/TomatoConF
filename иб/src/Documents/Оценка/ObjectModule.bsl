#Область ПроведениеДокумента

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверка на задвоение строк
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("Свиноматка");
	ДанныеЗадвоены = ОбщегоНазначенияСервер.ПроверитьДокументНаЗадвоениеСтрок(ЭтотОбъект, "Свиноматки", МассивКолонок);
	если ДанныеЗадвоены тогда
		Отказ = истина;
	КонецЕсли;
	МассивКолонок[0] = "Хряк";
	ДанныеЗадвоены = ОбщегоНазначенияСервер.ПроверитьДокументНаЗадвоениеСтрок(ЭтотОбъект, "Хряки", МассивКолонок);
	если ДанныеЗадвоены тогда
		Отказ = истина;
	КонецЕсли;	
	если Отказ тогда
		Возврат;
	КонецЕсли;
		
	ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ);
	
	Движения.сжсвИсторияСвиноматок.Записывать = истина;
	Движения.сжсвИсторияХряков.Записывать = истина;
	Движения.сжсвБонитировкаСвиноматокСтарая.Записывать = истина;
	Движения.сжсвБонитировкаХряковСтарая.Записывать = истина;
	Движения.сжсвБонитировкаСвиноматок.Записывать = истина;
	Движения.сжсвБонитировкаХряков.Записывать = истина;
	
	НормаКормленияРемонтногоМолодняка = Константы.сжсвНормаКормленияРемонтногоМолодняка.Получить();
    СреднесуточныйПривесВ100кгСвиноматки = Константы.сжсвСреднесуточныйПривесВ100кгСвиноматки.Получить()*0.001;
	СреднесуточныйПривесВ100кгСвиноматки = ?(СреднесуточныйПривесВ100кгСвиноматки=0,0.68,СреднесуточныйПривесВ100кгСвиноматки);
    СреднесуточныйПривесВ100кгХряка = Константы.сжсвСреднесуточныйПривесВ100кгХряка.Получить()*0.001;
	СреднесуточныйПривесВ100кгХряка = ?(СреднесуточныйПривесВ100кгХряка=0,0.72,СреднесуточныйПривесВ100кгХряка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сжсвОценкаСвиноматки.Свиноматка КАК Свиноматка,
	               |	ЗНАЧЕНИЕ(Справочник.сжсвХряки.ПустаяСсылка) КАК Хряк,
	               |	1 КАК Количество,
	               |	сжсвОценкаСвиноматки.Масса,
	               |	сжсвОценкаСвиноматки.ДлинаТуловища,
	               |	сжсвОценкаСвиноматки.ТолщинаШпига,
	               |	сжсвОценкаСвиноматки.Экстерьер,
	               |	NULL КАК ОбщийВид,
	               |	NULL КАК ГоловаШея,
	               |	NULL КАК ПлечиХолкаГрудь,
	               |	NULL КАК СпинаПоясницаБока,
	               |	NULL КАК КрестецОкорока,
	               |	NULL КАК НогиПередние,
	               |	NULL КАК НогиЗадние,
	               |	NULL КАК СоскиВымя,
	               |	NULL КАК ПоловыеОрганы,
	               |	сжсвОценкаСвиноматки.ГлубинаМышцы,
	               |	сжсвОценкаСвиноматки.ТолщинаШпига1,
	               |	сжсвОценкаСвиноматки.ТолщинаШпига2,
				   |	сжсвОценкаСвиноматки.ТолщинаШпига3,
	               |	сжсвОценкаСвиноматки.НомерСтроки,
	               |	сжсвОценкаСвиноматки.Ссылка.Дата КАК ДатаДокумента
	               |ПОМЕСТИТЬ ДанныеДокумента
	               |ИЗ
	               |	Документ.Оценка.Свиноматки КАК сжсвОценкаСвиноматки
	               |ГДЕ
	               |	сжсвОценкаСвиноматки.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.сжсвСвиноматки.ПустаяСсылка),
	               |	сжсвОценкаХряки.Хряк,
	               |	1,
	               |	сжсвОценкаХряки.Масса,
	               |	сжсвОценкаХряки.ДлинаТуловища,
	               |	сжсвОценкаХряки.ТолщинаШпига,
	               |	сжсвОценкаХряки.Экстерьер,
	               |	сжсвОценкаХряки.ОбщийВид,
	               |	сжсвОценкаХряки.ГоловаШея,
	               |	сжсвОценкаХряки.ПлечиХолкаГрудь,
	               |	сжсвОценкаХряки.СпинаПоясницаБока,
	               |	сжсвОценкаХряки.КрестецОкорока,
	               |	сжсвОценкаХряки.НогиПередние,
	               |	сжсвОценкаХряки.НогиЗадние,
	               |	сжсвОценкаХряки.СоскиВымя,
	               |	сжсвОценкаХряки.ПоловыеОрганы,
	               |	сжсвОценкаХряки.ГлубинаМышцы,
	               |	сжсвОценкаХряки.ТолщинаШпига1,
	               |	сжсвОценкаХряки.ТолщинаШпига2,
				   |	сжсвОценкаХряки.ТолщинаШпига3,
	               |	сжсвОценкаХряки.НомерСтроки,
	               |	сжсвОценкаХряки.Ссылка.Дата
	               |ИЗ
	               |	Документ.Оценка.Хряки КАК сжсвОценкаХряки
	               |ГДЕ
	               |	сжсвОценкаХряки.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	сжсвЖивотныеВНаличииОстатки.Ферма,
	               |	сжсвЖивотныеВНаличииОстатки.Сектор,
	               |	сжсвЖивотныеВНаличииОстатки.Станок,
	               |	сжсвЖивотныеВНаличииОстатки.Свиноматка,
	               |	сжсвЖивотныеВНаличииОстатки.Хряк,
	               |	сжсвЖивотныеВНаличииОстатки.ГруппаЖивотных,
	               |	сжсвЖивотныеВНаличииОстатки.ТехнолГруппа,
	               |	сжсвЖивотныеВНаличииОстатки.Группировка,
	               |	сжсвЖивотныеВНаличииОстатки.Партия,
	               |	сжсвЖивотныеВНаличииОстатки.КоличествоОстаток,
	               |	сжсвЖивотныеВНаличииОстатки.МассаОстаток
	               |ПОМЕСТИТЬ Остатки
	               |ИЗ
	               |	РегистрНакопления.сжсвЖивотныеВНаличии.Остатки(&МоментВремени, Ферма = &Ферма) КАК сжсвЖивотныеВНаличииОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДокумента.Свиноматка,
	               |	ДанныеДокумента.Хряк,
	               |	ДанныеДокумента.Количество КАК КоличествоДокумент,
	               |	ДанныеДокумента.Масса КАК МассаДокумент,
	               |	ДанныеДокумента.ДлинаТуловища,
	               |	ДанныеДокумента.ТолщинаШпига,
	               |	ДанныеДокумента.Экстерьер,
	               |	ДанныеДокумента.ОбщийВид,
	               |	ДанныеДокумента.ГоловаШея,
	               |	ДанныеДокумента.ПлечиХолкаГрудь,
	               |	ДанныеДокумента.СпинаПоясницаБока,
	               |	ДанныеДокумента.КрестецОкорока,
	               |	ДанныеДокумента.НогиПередние,
	               |	ДанныеДокумента.НогиЗадние,
	               |	ДанныеДокумента.СоскиВымя,
	               |	ДанныеДокумента.ПоловыеОрганы,
	               |	ДанныеДокумента.ГлубинаМышцы,
	               |	ДанныеДокумента.ТолщинаШпига1,
	               |	ДанныеДокумента.ТолщинаШпига2,
				   |	ДанныеДокумента.ТолщинаШпига3,
	               |	Остатки.Сектор,
	               |	Остатки.Станок,
	               |	Остатки.ГруппаЖивотных,
	               |	Остатки.ТехнолГруппа,
	               |	Остатки.Группировка,
	               |	Остатки.Партия,
	               |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	               |	ЕСТЬNULL(Остатки.МассаОстаток, 0) КАК МассаОстаток,
	               |	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	               |	ВЫБОР
	               |		КОГДА НЕ ДанныеДокумента.Свиноматка = ЗНАЧЕНИЕ(Справочник.сжсвСвиноматки.ПустаяСсылка)
	               |			ТОГДА ДанныеДокумента.Свиноматка.Представление
	               |		ИНАЧЕ ДанныеДокумента.Хряк.Представление
	               |	КОНЕЦ КАК ЖивотноеПредставление,
	               |	ЕСТЬNULL(сжсвОценкаСвиноматокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОценкиСв,
	               |	ЕСТЬNULL(сжсвОценкаСвиноматокСрезПоследних.Масса, 0) КАК МассаПриОценкеСв,
	               |	РАЗНОСТЬДАТ(сжсвОценкаСвиноматокСрезПоследних.Период, ДанныеДокумента.ДатаДокумента, ДЕНЬ) + 1 КАК КормоднейСв,
	               |	ЕСТЬNULL(сжсвОценкаХряковСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОценкиХр,
	               |	ЕСТЬNULL(сжсвОценкаХряковСрезПоследних.Масса, 0) КАК МассаПриОценкеХр,
	               |	РАЗНОСТЬДАТ(сжсвОценкаХряковСрезПоследних.Период, ДанныеДокумента.ДатаДокумента, ДЕНЬ) + 1 КАК КормоднейХр,
	               |	РАЗНОСТЬДАТ(ДанныеДокумента.Свиноматка.ДатаРождения, ДанныеДокумента.ДатаДокумента, ДЕНЬ) + 1 КАК ВозрастДнейСв,
	               |	РАЗНОСТЬДАТ(ДанныеДокумента.Хряк.ДатаРождения, ДанныеДокумента.ДатаДокумента, ДЕНЬ) + 1 КАК ВозрастДнейХр,
	               |	ДанныеДокумента.Свиноматка.ДатаРождения,
	               |	ДанныеДокумента.Хряк.ДатаРождения
	               |ПОМЕСТИТЬ ПредвДанные
	               |ИЗ
	               |	ДанныеДокумента КАК ДанныеДокумента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	               |		ПО ДанныеДокумента.Свиноматка = Остатки.Свиноматка
	               |			И ДанныеДокумента.Хряк = Остатки.Хряк
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сжсвОценкаСвиноматок.СрезПоследних(
	               |				&МоментВремени,
	               |				Свиноматка В
	               |						(ВЫБРАТЬ
	               |							ДанныеДокумента.Свиноматка
	               |						ИЗ
	               |							ДанныеДокумента)
	               |					И ТипОценки = ЗНАЧЕНИЕ(Перечисление.сжсвТипОценки.НаВыращивании)) КАК сжсвОценкаСвиноматокСрезПоследних
	               |		ПО ДанныеДокумента.Свиноматка = сжсвОценкаСвиноматокСрезПоследних.Свиноматка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сжсвОценкаХряков.СрезПоследних(
	               |				&МоментВремени,
	               |				Хряк В
	               |						(ВЫБРАТЬ
	               |							ДанныеДокумента.Хряк
	               |						ИЗ
	               |							ДанныеДокумента)
	               |					И ТипОценки = ЗНАЧЕНИЕ(Перечисление.сжсвТипОценки.НаВыращивании)) КАК сжсвОценкаХряковСрезПоследних
	               |		ПО ДанныеДокумента.Хряк = сжсвОценкаХряковСрезПоследних.Хряк
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПредвДанные.Свиноматка,
	               |	ПредвДанные.Хряк,
	               |	ПредвДанные.МассаДокумент,
	               |	ПредвДанные.ДлинаТуловища,
	               |	ПредвДанные.ТолщинаШпига,
	               |	ПредвДанные.Экстерьер,
	               |	ПредвДанные.ОбщийВид,
	               |	ПредвДанные.ГоловаШея,
	               |	ПредвДанные.ПлечиХолкаГрудь,
	               |	ПредвДанные.СпинаПоясницаБока,
	               |	ПредвДанные.КрестецОкорока,
	               |	ПредвДанные.НогиПередние,
	               |	ПредвДанные.НогиЗадние,
	               |	ПредвДанные.СоскиВымя,
	               |	ПредвДанные.ПоловыеОрганы,
	               |	ПредвДанные.ГлубинаМышцы,
	               |	ПредвДанные.ТолщинаШпига1,
	               |	ПредвДанные.ТолщинаШпига2,
				   |	ПредвДанные.ТолщинаШпига3,
	               |	ПредвДанные.Сектор,
	               |	ПредвДанные.Станок,
	               |	ПредвДанные.ГруппаЖивотных,
	               |	ПредвДанные.ТехнолГруппа,
	               |	ПредвДанные.Группировка,
	               |	ПредвДанные.Партия,
	               |	ПредвДанные.КоличествоОстаток,
	               |	ПредвДанные.МассаОстаток,
	               |	ПредвДанные.НомерСтроки,
	               |	ПредвДанные.ЖивотноеПредставление,
	               |	ПредвДанные.ДатаОценкиСв,
	               |	ПредвДанные.МассаПриОценкеСв,
	               |	ПредвДанные.КормоднейСв,
	               |	ПредвДанные.ДатаОценкиХр,
	               |	ПредвДанные.МассаПриОценкеХр,
	               |	ПредвДанные.КормоднейХр,
	               |	ПредвДанные.ВозрастДнейСв,
	               |	ПредвДанные.ВозрастДнейХр,
	               |	ПредвДанные.СвиноматкаДатаРождения,
	               |	ПредвДанные.ХрякДатаРождения,
	               |	ПредвДанные.КоличествоДокумент
	               |ИЗ
	               |	ПредвДанные КАК ПредвДанные
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПредвДанные.НомерСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПредвДанные.Сектор.Владелец КАК Корпус,
	               |	ПредвДанные.Сектор,
	               |	ПредвДанные.ГруппаЖивотных КАК ГруппаОткуда,
	               |	ВЫБОР
	               |		КОГДА НЕ ПредвДанные.Свиноматка = ЗНАЧЕНИЕ(Справочник.сжсвСвиноматки.ПустаяСсылка)
	               |			ТОГДА ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеСвинки)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |	КОНЕЦ КАК ГруппаКуда,
	               |	ПредвДанные.ТехнолГруппа,
	               |	ПредвДанные.Группировка,
	               |	МАКСИМУМ(ЕСТЬNULL(сжсвПереводИзГруппыВГруппу.Ссылка, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка))) КАК ДокументСсылка
	               |ИЗ
	               |	ПредвДанные КАК ПредвДанные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.сжсвПереводИзГруппыВГруппу КАК сжсвПереводИзГруппыВГруппу
	               |		ПО ПредвДанные.Сектор = сжсвПереводИзГруппыВГруппу.Сектор
	               |			И ПредвДанные.ТехнолГруппа = сжсвПереводИзГруппыВГруппу.ТехнолГруппа
	               |			И ПредвДанные.Группировка = сжсвПереводИзГруппыВГруппу.Группировка
	               |			И ПредвДанные.ГруппаЖивотных = сжсвПереводИзГруппыВГруппу.Группа
	               |			И (сжсвПереводИзГруппыВГруппу.ДокументОснование = &Ссылка)
	               |			И (ВЫБОР
	               |				КОГДА НЕ ПредвДанные.Свиноматка = ЗНАЧЕНИЕ(Справочник.сжсвСвиноматки.ПустаяСсылка)
	               |					ТОГДА сжсвПереводИзГруппыВГруппу.ГруппаКуда = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеСвинки)
	               |				ИНАЧЕ сжсвПереводИзГруппыВГруппу.ГруппаКуда = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |			КОНЕЦ)
	               |ГДЕ
	               |	ПредвДанные.ГруппаЖивотных = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.Группа_1_3)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПредвДанные.Сектор.Владелец,
	               |	ПредвДанные.Сектор,
	               |	ПредвДанные.ГруппаЖивотных,
	               |	ПредвДанные.ТехнолГруппа,
	               |	ПредвДанные.Группировка,
	               |	ВЫБОР
	               |		КОГДА НЕ ПредвДанные.Свиноматка = ЗНАЧЕНИЕ(Справочник.сжсвСвиноматки.ПустаяСсылка)
	               |			ТОГДА ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеСвинки)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |	КОНЕЦ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ферма", Ферма);
	РезультатПакет = Запрос.ВыполнитьПакет();
	Выборка = РезультатПакет[3].Выбрать();
	ВыборкаРазрезыПеревода = РезультатПакет[4].Выбрать();
	
	пока Выборка.Следующий() цикл
		если Выборка.КоличествоДокумент > Выборка.КоличествоОстаток тогда
			ТекстСообщения = ?(ЗначениеЗаполнено(Выборка.Свиноматка), "Свиноматка", "Хряк") + " номер " + Выборка.ЖивотноеПредставление +
				" не находится на указанной ферме";
			если ЗначениеЗаполнено(Выборка.Свиноматка) тогда
				Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
			иначе
				Поле = "Хряки[" + (Выборка.НомерСтроки - 1) + "].Хряк";
			КонецЕсли;
			ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
			Отказ = истина;		
			Продолжить;
		КонецЕсли;
		
		если НЕ Выборка.МассаДокумент = Выборка.МассаОстаток тогда
			// регистр ЖивотныеВНаличии Приход
			Движение = Движения.сжсвЖивотныеВНаличии.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Ферма = Ферма;
			Движение.Сектор = Выборка.Сектор;
			Движение.Станок = Выборка.Станок;
			Движение.ГруппаЖивотных = Выборка.ГруппаЖивотных;
			Движение.Группировка = Выборка.Группировка;
			Движение.ТехнолГруппа = Выборка.ТехнолГруппа;
			Движение.Свиноматка = Выборка.Свиноматка;
			Движение.Хряк = Выборка.Хряк;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = 0;
			Движение.Масса = Выборка.МассаДокумент - Выборка.МассаОстаток;
			Движение.Причина = Перечисления.сжсвПричиныДвиженияПоголовья.Привес;
			
			если ЗначениеЗаполнено (Выборка.ТехнолГруппа) тогда
				Движение = Движения.сжсвДвижениеПоТехнолГруппам.Добавить();
				Движение.Период = Дата;
				Движение.Причина = Перечисления.сжсвПричиныДвиженияПоголовья.Привес;
				Движение.Ферма = Ферма;
				Движение.Сектор = Выборка.Сектор;
				Движение.Станок = Выборка.Станок;
				Движение.ГруппаЖивотных = Выборка.ГруппаЖивотных;
				Движение.Группировка = Выборка.Группировка;
				Движение.ТехнолГруппа = Выборка.ТехнолГруппа;
				Движение.Свиноматка = Выборка.Свиноматка;
				Движение.Хряк = Выборка.Хряк;
				Движение.Количество = 0;
				Движение.Масса = Выборка.МассаДокумент - Выборка.МассаОстаток;					
			КонецЕсли;
		КонецЕсли;
	
		если ЗначениеЗаполнено(Выборка.Свиноматка) тогда						
			// Регистр ОценкаСвиноматок 
			Движение = Движения.сжсвОценкаСвиноматок.Добавить();
			Движение.Период = Дата;
			Движение.Свиноматка = Выборка.Свиноматка;
			Движение.ТипОценки = ТипОценки;
			Движение.Ферма = Ферма;
			Если ТипОценки = Перечисления.сжсвТипОценки.В100кг Тогда
				Движение.Масса = Выборка.МассаДокумент;
				Движение.ДлинаТуловища = Выборка.ДлинаТуловища + (100 - Выборка.МассаДокумент)*0.35;
				Движение.Экстерьер = Выборка.Экстерьер;
				Движение.ТолщинаШпига = Выборка.ТолщинаШпига + (100 - Выборка.МассаДокумент)*0.15;
				Движение.ГлубинаМышцы = Выборка.ГлубинаМышцы + (100 - Выборка.МассаДокумент)*0.25;
				Движение.ТолщинаШпига1 = Выборка.ТолщинаШпига1 + (100 - Выборка.МассаДокумент)*0.15;
				Движение.ТолщинаШпига2 = Выборка.ТолщинаШпига2;
				Движение.ТолщинаШпига3 = Выборка.ТолщинаШпига3;
				если ЗначениеЗаполнено(Выборка.СвиноматкаДатаРождения) тогда
					Движение.Возраст = (Дата - Выборка.СвиноматкаДатаРождения)/86400 + (100 - Выборка.МассаДокумент)/СреднесуточныйПривесВ100кгСвиноматки;
				КонецЕсли;			
				
				Если ЗначениеЗаполнено(Выборка.ДатаОценкиСв) И ЗначениеЗаполнено (Выборка.МассаПриОценкеСв) тогда
					Движение.Привес = Выборка.МассаДокумент - Выборка.МассаПриОценкеСв;
					Движение.Кормодней = Выборка.КормоднейСв;
					Движение.КонверсияКорма = ?(Движение.Привес = 0, 0, Выборка.КормоднейСв*НормаКормленияРемонтногоМолодняка/Движение.Привес);
				КонецЕсли;
			Иначе
				Движение.Масса = Выборка.МассаДокумент;
				Движение.ДлинаТуловища = Выборка.ДлинаТуловища;
				Движение.Экстерьер = Выборка.Экстерьер;
				Движение.ТолщинаШпига = Выборка.ТолщинаШпига;
				если ЗначениеЗаполнено(Выборка.СвиноматкаДатаРождения) тогда
					Движение.Возраст = Выборка.ВозрастДнейСв;
				КонецЕсли;
				Движение.ГлубинаМышцы = Выборка.ГлубинаМышцы;
				Движение.ТолщинаШпига1 = Выборка.ТолщинаШпига1;
				Движение.ТолщинаШпига2 = Выборка.ТолщинаШпига2;
				Движение.ТолщинаШпига3 = Выборка.ТолщинаШпига3;
			КонецЕсли;
			// регистр ИсторияСвиноматок
			Движение = Движения.сжсвИсторияСвиноматок.Добавить();
			Движение.Период = Дата;
			Движение.Свиноматка = Выборка.Свиноматка;
			Движение.Событие = Перечисления.сжсвСобытия.Оценка;
			Движение.Документ = Ссылка;
			Движение.ДатаСобытия = Дата;
			
		иначеЕсли ЗначениеЗаполнено (Выборка.Хряк) тогда
			// регистр ОценкаХряков 
			Движение = Движения.сжсвОценкаХряков.Добавить();
			Движение.Период = Дата;
			Движение.Хряк = Выборка.Хряк;
			Движение.ТипОценки = ТипОценки;
			Движение.Ферма = Ферма;
			Если ТипОценки = Перечисления.сжсвТипОценки.В100кг Тогда
				Движение.Масса = Выборка.МассаДокумент;
				Движение.ДлинаТуловища = Выборка.ДлинаТуловища + (100 - Выборка.МассаДокумент)*0.35;
				Движение.Экстерьер = Выборка.Экстерьер;
				Движение.ГлубинаМышцы = Выборка.ГлубинаМышцы + (100 - Выборка.МассаДокумент)*0.25;
				Движение.ТолщинаШпига1 = Выборка.ТолщинаШпига1 + (100 - Выборка.МассаДокумент)*0.15;
				Движение.ТолщинаШпига2 = Выборка.ТолщинаШпига2;
				Движение.ТолщинаШпига3 = Выборка.ТолщинаШпига3;
				Движение.ТолщинаШпига = Выборка.ТолщинаШпига + (100 - Выборка.МассаДокумент)*0.15;
				если ЗначениеЗаполнено(Выборка.ХрякДатаРождения) тогда
					Движение.Возраст = (Дата - Выборка.ХрякДатаРождения)/86400 + (100 - Выборка.МассаДокумент)/СреднесуточныйПривесВ100кгХряка;
				КонецЕсли;
								
				Если ЗначениеЗаполнено(Выборка.ДатаОценкиХр) И ЗначениеЗаполнено (Выборка.МассаПриОценкеХр) тогда
					Движение.Привес = Выборка.МассаДокумент - Выборка.МассаПриОценкеХр;
					Движение.Кормодней = Выборка.КормоднейХр;
					Движение.КонверсияКорма = Выборка.КормоднейХр*НормаКормленияРемонтногоМолодняка/Движение.Привес;
				КонецЕсли;
			Иначе
				Движение.Масса = Выборка.МассаДокумент;
				Движение.ДлинаТуловища = Выборка.ДлинаТуловища;
				Движение.Экстерьер = Выборка.Экстерьер;
				Движение.ТолщинаШпига = Выборка.ТолщинаШпига;
				Движение.ГлубинаМышцы = Выборка.ГлубинаМышцы;
				Движение.ТолщинаШпига1 = Выборка.ТолщинаШпига1;
				Движение.ТолщинаШпига2 = Выборка.ТолщинаШпига2;
				Движение.ТолщинаШпига3 = Выборка.ТолщинаШпига3;
				если ЗначениеЗаполнено(Выборка.ХрякДатаРождения) тогда
					Движение.Возраст = Выборка.ВозрастДнейХр;
				КонецЕсли;
			КонецЕсли;
			Движение.общийВид = Выборка.общийВид;
			Движение.ГоловаШея = Выборка.ГоловаШея;
			Движение.ПлечиХолкаГрудь = Выборка.ПлечиХолкаГрудь;
			Движение.СпинаПоясницаБока = Выборка.СпинаПоясницаБока;
			Движение.КрестецОкорока = Выборка.КрестецОкорока;
			Движение.НогиПередние = Выборка.НогиПередние;
			Движение.НогиЗадние = Выборка.НогиЗадние;
			Движение.СоскиВымя = Выборка.СоскиВымя;
			Движение.ПоловыеОрганы = Выборка.ПоловыеОрганы;
			
			// регистр ИсторияХряков
			Движение = Движения.сжсвИсторияХряков.Добавить();
			Движение.Период = Дата;
			Движение.Хряк = Выборка.Хряк;
			Движение.Событие = Перечисления.сжсвСобытия.Оценка;
			Движение.Документ = ЭтотОбъект.Ссылка;
			Движение.ДатаСобытия = Дата;
		КонецЕсли;
	КонецЦикла;

	Движения.сжсвЖивотныеВНаличии.Записать();
	Движения.сжсвДвижениеПоТехнолГруппам.Записать();
	Движения.сжсвОценкаСвиноматок.Записать();
	Движения.сжсвОценкаХряков.Записать();
	
	Если (ТипОценки = Перечисления.сжсвТипОценки.НаВыращивании) ИЛИ (ТипОценки = Перечисления.сжсвТипОценки.В100кг)
			ИЛИ (ТипОценки = Перечисления.сжсвТипОценки.ОценкаРазвития) Тогда
		// проводим оценку свиноматок
		Для Каждого Строка Из Свиноматки Цикл
			ПоказателиСвиноматки = сжсвОценка.ОценкаСвиноматки75(Дата, Строка.Свиноматка);
			
			Движение = Движения.сжсвБонитировкаСвиноматокСтарая.Добавить();
			Движение.Период = Дата;
			Движение.Свиноматка = Строка.Свиноматка;
			Если ТипОценки = Перечисления.сжсвТипОценки.ОценкаРазвития Тогда
				Движение.ОценкаРазвития = Истина;
			КонецЕсли;
			Движение.Дата = Дата;
			Движение.Возраст = ПоказателиСвиноматки.Возраст;
			Движение.Масса = ПоказателиСвиноматки.Масса;
			Движение.БаллПоМассе = ПоказателиСвиноматки.БаллПоМассе;
			Движение.ДлинаТуловища = ПоказателиСвиноматки.ДлинаТуловища;
			Движение.БаллПоДлинеТуловища = ПоказателиСвиноматки.БаллПоДлинеТуловища;
			Движение.ВозрастВ100кг = ПоказателиСвиноматки.ВозрастВ100кг;
			Движение.МассаВ100кг = ПоказателиСвиноматки.МассаВ100кг;
			Движение.ТолщинаШпига = ПоказателиСвиноматки.ТолщинаШпига;
			Движение.ТолщинаШпига1 = ПоказателиСвиноматки.ТолщинаШпига1;
			Движение.ТолщинаШпига2 = ПоказателиСвиноматки.ТолщинаШпига2;
			Движение.ТолщинаШпига3 = Строка.ТолщинаШпига3;
            Движение.ГлубинаМышцы = ПоказателиСвиноматки.ГлубинаМышцы;
			Движение.БаллПоТолщинеШпига = ПоказателиСвиноматки.БаллПоТолщинеШпига;
			Движение.Экстерьер = ПоказателиСвиноматки.Экстерьер;
			Движение.БаллПоЭкстерьеру = ПоказателиСвиноматки.БаллПоЭкстерьеру;
			Движение.КоличествоОпоросов = ПоказателиСвиноматки.КоличествоОпоросов;
			Движение.Многоплодие = ПоказателиСвиноматки.Многоплодие;
			Движение.БаллПоМногоплодию = ПоказателиСвиноматки.БаллПоМногоплодию;
			Движение.Молочность21 = ПоказателиСвиноматки.Молочность21;
			Движение.БаллПоМолочности21 = ПоказателиСвиноматки.БаллПоМолочности21;
			Движение.Количество60 = ПоказателиСвиноматки.Количество60;
			Движение.Молочность60 = ПоказателиСвиноматки.Молочность60;
			Движение.БаллПоМолочности60 = ПоказателиСвиноматки.БаллПоМолочности60;
			Движение.Масса60 = ПоказателиСвиноматки.Масса60;
			Движение.СреднийБалл = ПоказателиСвиноматки.СреднийБалл;
			Движение.СуммарныйБалл = ПоказателиСвиноматки.СуммарныйБалл;
			Движение.Класс =  сжсвОценка.КлассПоБаллу75(ПоказателиСвиноматки.СреднийБалл);
			Движение.Ферма = Ферма;
			
			ПоказателиСвиноматки = сжсвОценка.ОценкаСвиноматки(Дата+1, Строка.Свиноматка);
			КлассСвиноматки = ПоказателиСвиноматки.Класс;
			Если ЗначениеЗаполнено(КлассСвиноматки) Тогда
				Свиноматка = Строка.Свиноматка.ПолучитьОбъект();
				Свиноматка.Класс = КлассСвиноматки;
				Свиноматка.Записать();
			КонецЕсли;
			
			Движение = Движения.сжсвБонитировкаСвиноматок.Добавить();
			Движение.Период = Дата;
			Движение.Свиноматка = Строка.Свиноматка;
			Если ТипОценки = Перечисления.сжсвТипОценки.ОценкаРазвития Тогда
				Движение.ОценкаРазвития = Истина;
			КонецЕсли;
			Движение.Дата = Дата;
			Для Каждого Показатель Из ПоказателиСвиноматки Цикл
				Движение[Показатель.Ключ]=Показатель.Значение;
			КонецЦикла;
			Движение.Ферма = Ферма;
		КонецЦикла;
		
		// проводим оценку хряка	
		Для Каждого Строка Из Хряки Цикл
			ПоказателиХряка = сжсвОценка.ОценкаХряка75(Дата, Строка.Хряк);
			Движение = Движения.сжсвБонитировкаХряковСтарая.Добавить();
			Движение.Период = Дата;
			Движение.Хряк = Строка.Хряк;
			Если ТипОценки = Перечисления.сжсвТипОценки.ОценкаРазвития Тогда
				Движение.ОценкаРазвития = Истина;
			КонецЕсли;
			Движение.Дата = Дата;
			Движение.Возраст = ПоказателиХряка.Возраст;
			Движение.Масса = ПоказателиХряка.Масса;
			Движение.БаллПоМассе = ПоказателиХряка.БаллПоМассе;
			Движение.ДлинаТуловища = ПоказателиХряка.ДлинаТуловища;
			Движение.БаллПоДлинеТуловища = ПоказателиХряка.БаллПоДлинеТуловища;
			Движение.ВозрастВ100кг = ПоказателиХряка.ВозрастВ100кг;
			Движение.МассаВ100кг = ПоказателиХряка.МассаВ100кг;
			Движение.ТолщинаШпига = ПоказателиХряка.ТолщинаШпига;
			Движение.БаллПоТолщинеШпига = ПоказателиХряка.БаллПоТолщинеШпига;
			Движение.Экстерьер = ПоказателиХряка.Экстерьер;
			Движение.БаллПоЭкстерьеру = ПоказателиХряка.БаллПоЭкстерьеру;
			Движение.ВсегоОсеменений = ПоказателиХряка.ВсегоОсеменений;
			Движение.ВсегоПоложительныхРезультатов = ПоказателиХряка.ВсегоПоложительныхРезультатов;
			Движение.Многоплодие = ПоказателиХряка.Многоплодие;
			Движение.Количество60 = ПоказателиХряка.Количество60;
			Движение.МассаПотомка60 = ПоказателиХряка.МассаПотомка60;
			Движение.БаллПоМассеПотомка60 = ПоказателиХряка.БаллПоМассеПотомка60;
			Движение.СреднийБалл = ПоказателиХряка.СреднийБалл;
			Движение.СуммарныйБалл = ПоказателиХряка.СуммарныйБалл;
			Движение.Класс =  сжсвОценка.КлассПоБаллу75(ПоказателиХряка.СреднийБалл);
			Движение.ТолщинаШпига1 = ПоказателиХряка.ТолщинаШпига1;
			Движение.ТолщинаШпига2 = ПоказателиХряка.ТолщинаШпига2;
			Движение.ТолщинаШпига3 = Строка.ТолщинаШпига3;
            Движение.ГлубинаМышцы = ПоказателиХряка.ГлубинаМышцы;
			Движение.Ферма = Ферма;

			ПоказателиХряка = сжсвОценка.ОценкаХряка(Дата+1, Строка.Хряк);
			КлассХряка = ПоказателиХряка.Класс;
			Если ЗначениеЗаполнено(КлассХряка) Тогда
				Хряк = Строка.Хряк.ПолучитьОбъект();
				Хряк.Класс = КлассХряка;
				Хряк.Записать();
			КонецЕсли;
			Движение = Движения.сжсвБонитировкаХряков.Добавить();
			Движение.Период = Дата;
			Движение.Хряк = Строка.Хряк;
			Если ТипОценки = Перечисления.сжсвТипОценки.ОценкаРазвития Тогда
				Движение.ОценкаРазвития = Истина;
			КонецЕсли;
			Движение.Дата = Дата;
			Для Каждого Показатель Из ПоказателиХряка Цикл
				Движение[Показатель.Ключ]=Показатель.Значение;
			КонецЦикла;
			Движение.Ферма = Ферма;
		КонецЦикла;
	КонецЕсли;
	
	УсловиеПоиска = Новый Структура ("Сектор, ГруппаЖивотных, ТехнолГруппа, Группировка");
	пока ВыборкаРазрезыПеревода.Следующий() цикл
		если ЗначениеЗаполнено(ВыборкаРазрезыПеревода.ДокументСсылка) тогда
			ПереводОбъект = ВыборкаРазрезыПеревода.ДокументСсылка.ПолучитьОбъект();
			ВидДействия = "Перезаполнен ";
			ПереводОбъект.Свиноматки.Очистить();
			ПереводОбъект.Хряки.Очистить();
			
			ПереводОбъект.Дата = Дата+1;
			ПереводОбъект.Ферма = Ферма;
			ПереводОбъект.СформированАвтоматически = Истина;
			ПереводОбъект.Корпус = ВыборкаРазрезыПеревода.Корпус;
			ПереводОбъект.КорпусКуда = ВыборкаРазрезыПеревода.Корпус;
			ПереводОбъект.Сектор = ВыборкаРазрезыПеревода.Сектор;
			ПереводОбъект.СекторКуда = ВыборкаРазрезыПеревода.Сектор;
			ПереводОбъект.Группировка = ВыборкаРазрезыПеревода.Группировка;
			ПереводОбъект.ГруппировкаКуда = ВыборкаРазрезыПеревода.Группировка;
			ПереводОбъект.ТехнолГруппа = ВыборкаРазрезыПеревода.ТехнолГруппа;
			ПереводОбъект.ТехнолГруппаКуда = ВыборкаРазрезыПеревода.ТехнолГруппа;
			ПереводОбъект.Группа = ВыборкаРазрезыПеревода.ГруппаОткуда;
			ПереводОбъект.ГруппаКуда = ВыборкаРазрезыПеревода.ГруппаКуда;
			ПереводОбъект.ДокументОснование = Ссылка;
		иначе
			ПереводОбъект = Документы.сжсвПереводИзГруппыВГруппу.СоздатьДокумент();
			ВидДействия = "Создан ";
			ПереводОбъект.Дата = Дата+1;
			ПереводОбъект.Ферма = Ферма;
			ПереводОбъект.СформированАвтоматически = Истина;
			ПереводОбъект.Корпус = ВыборкаРазрезыПеревода.Корпус;
			ПереводОбъект.КорпусКуда = ВыборкаРазрезыПеревода.Корпус;
			ПереводОбъект.Сектор = ВыборкаРазрезыПеревода.Сектор;
			ПереводОбъект.СекторКуда = ВыборкаРазрезыПеревода.Сектор;
			ПереводОбъект.Группировка = ВыборкаРазрезыПеревода.Группировка;
			ПереводОбъект.ГруппировкаКуда = ВыборкаРазрезыПеревода.Группировка;
			ПереводОбъект.ТехнолГруппа = ВыборкаРазрезыПеревода.ТехнолГруппа;
			ПереводОбъект.ТехнолГруппаКуда = ВыборкаРазрезыПеревода.ТехнолГруппа;
			ПереводОбъект.Группа = ВыборкаРазрезыПеревода.ГруппаОткуда;
			ПереводОбъект.ГруппаКуда = ВыборкаРазрезыПеревода.ГруппаКуда;
			ПереводОбъект.ДокументОснование = Ссылка;
		КонецЕсли;
		
		УсловиеПоиска.Сектор = ВыборкаРазрезыПеревода.Сектор;
		УсловиеПоиска.ГруппаЖивотных = ВыборкаРазрезыПеревода.ГруппаОткуда;
		УсловиеПоиска.ТехнолГруппа = ВыборкаРазрезыПеревода.ТехнолГруппа;
		УсловиеПоиска.Группировка = ВыборкаРазрезыПеревода.Группировка;
		
		Выборка.Сбросить();
		пока Выборка.НайтиСледующий(УсловиеПоиска) цикл
			если ВыборкаРазрезыПеревода.ГруппаКуда = Перечисления.сжсвГруппыЖивотных.РемонтныеСвинки И
				НЕ ЗначениеЗаполнено(Выборка.Свиноматка) тогда
				Продолжить;
			иначеЕсли ВыборкаРазрезыПеревода.ГруппаКуда = Перечисления.сжсвГруппыЖивотных.РемонтныеХрячки И
				НЕ ЗначениеЗаполнено(Выборка.Хряк) тогда
				Продолжить;
			КонецЕсли;
			
			если ЗначениеЗаполнено(Выборка.Свиноматка) тогда
				НоваяСтрока = ПереводОбъект.Свиноматки.Добавить();
				НоваяСтрока.Свиноматка = Выборка.Свиноматка;
				НоваяСтрока.Масса = Выборка.МассаДокумент;
				НоваяСтрока.Станок = Выборка.Станок;
			иначе
				НоваяСтрока = ПереводОбъект.Хряки.Добавить();
				НоваяСтрока.Хряк = Выборка.Хряк;
				НоваяСтрока.Масса = Выборка.МассаДокумент;
				НоваяСтрока.Станок = Выборка.Станок;
			КонецЕсли;
		КонецЦикла;
		
		если НЕ ПереводОбъект = Неопределено тогда
			ПереводОбъект.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначенияСервер.СообщитьПользователю(ВидДействия + "документ " + ПереводОбъект, ПереводОбъект);
			Попытка
				ПереводОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначенияСервер.СообщитьПользователю("При попытке проведения возникла ошибка: " + ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ);
	
КонецПроцедуры

Процедура ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сжсвПереводИзГруппыВГруппу.Ссылка
	               |ИЗ
	               |	Документ.сжсвПереводИзГруппыВГруппу КАК сжсвПереводИзГруппыВГруппу
	               |ГДЕ
	               |	сжсвПереводИзГруппыВГруппу.ДокументОснование = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	пока Выборка.Следующий() цикл
		Попытка
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Записать (РежимЗаписиДокумента.ОтменаПроведения);
		Исключение
			Отказ = истина;
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти