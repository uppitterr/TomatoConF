#Область ПроведениеДокумента

Процедура ПриЗаписи(Отказ)
	
	Если НЕ ОбменДанными.Загрузка Тогда
		// Очистим от мусора
		лМассивУдаляемых = Новый Массив;
		Для каждого лСтрокаТЧ Из Состав Цикл
			Если Кормосмеси.НайтиСтроки(Новый Структура("Кормосмесь", лСтрокаТЧ.Кормосмесь)).Количество() = 0 Тогда
				лМассивУдаляемых.Добавить(лСтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
		Для каждого лСтрокаТЧ Из лМассивУдаляемых Цикл
			Состав.Удалить(лСтрокаТЧ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.сжсвПартииТоваровНаСкладах.Записывать = истина;
	
	МетодСписанияСебестоимости = Константы.сжсвМетодСписанияСебестоимости.Получить();
	МетодСписанияСебестоимости = ?(ЗначениеЗаполнено(МетодСписанияСебестоимости), МетодСписанияСебестоимости, Перечисления.сжсвМетодыСписанияСебестоимости.FIFO);
	Если МетодСписанияСебестоимости = Перечисления.сжсвМетодыСписанияСебестоимости.FIFO
		ИЛИ МетодСписанияСебестоимости = Перечисления.сжсвМетодыСписанияСебестоимости.ПоСредней Тогда
		ПорядокСортировки = " ВОЗР";
	Иначе
		ПорядокСортировки = " УБЫВ";
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	сжсвИзготовлениеКормосмесиСостав.Склад,
	|	сжсвИзготовлениеКормосмесиСостав.Номенклатура,
	|	СУММА(сжсвИзготовлениеКормосмесиСостав.Количество) КАК Количество,
	|	сжсвИзготовлениеКормосмесиСостав.Кормосмесь
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ИзготовлениеУдобрения.Состав КАК сжсвИзготовлениеКормосмесиСостав
	|ГДЕ
	|	сжсвИзготовлениеКормосмесиСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	сжсвИзготовлениеКормосмесиСостав.Склад,
	|	сжсвИзготовлениеКормосмесиСостав.Номенклатура,
	|	сжсвИзготовлениеКормосмесиСостав.Кормосмесь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	сжсвИзготовлениеКормосмесиКормосмеси.Кормосмесь,
	|	сжсвИзготовлениеКормосмесиКормосмеси.Количество КАК КоличествоКормосмеси,
	|	сжсвИзготовлениеКормосмесиКормосмеси.Склад КАК СкладКормосмеси
	|ПОМЕСТИТЬ Кормосмеси
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзготовлениеУдобрения.Кормосмеси КАК сжсвИзготовлениеКормосмесиКормосмеси
	|		ПО ДанныеДокумента.Кормосмесь = сжсвИзготовлениеКормосмесиКормосмеси.Кормосмесь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	сжсвПартииТоваровНаСкладахОстатки.Склад,
	|	сжсвПартииТоваровНаСкладахОстатки.Товар КАК Номенклатура,
	|	сжсвПартииТоваровНаСкладахОстатки.ДокументПоступления КАК Партия,
	|	сжсвПартииТоваровНаСкладахОстатки.КоличествоОстаток,
	|	сжсвПартииТоваровНаСкладахОстатки.СуммаОстаток,
	|	ВЫБОР
	|		КОГДА &МетодСписанияСебестоимости = ЗНАЧЕНИЕ(Перечисление.сжсвМетодыСписанияСебестоимости.ПоСредней)
	|			ТОГДА ВЫБОР
	|					КОГДА сжсвПартииТоваровНаСкладахОстатки.ДокументПоступления = ЗНАЧЕНИЕ(Документ.сжсвПоступлениеТоваров.ПустаяСсылка)
	|						ТОГДА 2
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА сжсвПартииТоваровНаСкладахОстатки.ДокументПоступления = ЗНАЧЕНИЕ(Документ.сжсвПоступлениеТоваров.ПустаяСсылка)
	|					ТОГДА 1
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК ПорядокПартии,
	|	сжсвПартииТоваровНаСкладахОстатки.Ферма
	|ПОМЕСТИТЬ Партии
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.сжсвПартииТоваровНаСкладах.Остатки(
	|				&ДатаДокумента,
	|				Склад В
	|					(ВЫБРАТЬ
	|						ДанныеДокумента.Склад
	|					ИЗ
	|						ДанныеДокумента)) КАК сжсвПартииТоваровНаСкладахОстатки
	|		ПО ДанныеДокумента.Номенклатура = сжсвПартииТоваровНаСкладахОстатки.Товар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Склад КАК Склад,
	|	Партии.Партия КАК Партия,
	|	ЕСТЬNULL(Партии.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(Партии.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(Партии.ПорядокПартии, 1) КАК ПорядокПартии,
	|	Партии.Ферма КАК ФермаПартия,
	|	ДанныеДокумента.Склад.Представление,
	|	ЕСТЬNULL(ВложенныйЗапрос1.КоличествоОстатокВсего, 0) КАК КоличествоОстатокВсего,
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Номенклатура.Представление,
	|	ДанныеДокумента.Склад.Ферма КАК ФермаКуда,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Номенклатура.Кормосмесь
	|			ТОГДА ""кормосмеси""
	|		ИНАЧЕ ""корма""
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ДанныеДокумента.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаХраненияОстатков,
	|	ВложенныйЗапрос.ВсегоНеобходимо,
	|	ДанныеДокумента.Номенклатура.ЕдиницаХраненияОстатков.Представление КАК ЕдИзмОстатков,
	|	ДанныеДокумента.Количество КАК КоличествоДокумент,
	|	ДанныеДокумента.Кормосмесь КАК Кормосмесь,
	|	Кормосмеси.КоличествоКормосмеси
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Партии КАК Партии
	|		ПО ДанныеДокумента.Склад = Партии.Склад
	|			И ДанныеДокумента.Номенклатура = Партии.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДанныеДокумента.Склад КАК Склад,
	|			ДанныеДокумента.Номенклатура КАК Номенклатура,
	|			СУММА(ДанныеДокумента.Количество) КАК ВсегоНеобходимо,
	|			ДанныеДокумента.Кормосмесь КАК Кормосмесь
	|		ИЗ
	|			ДанныеДокумента КАК ДанныеДокумента
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДанныеДокумента.Склад,
	|			ДанныеДокумента.Номенклатура,
	|			ДанныеДокумента.Кормосмесь) КАК ВложенныйЗапрос
	|		ПО ДанныеДокумента.Склад = ВложенныйЗапрос.Склад
	|			И ДанныеДокумента.Номенклатура = ВложенныйЗапрос.Номенклатура
	|			И ДанныеДокумента.Кормосмесь = ВложенныйЗапрос.Кормосмесь
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Партии.Склад КАК Склад,
	|			Партии.Номенклатура КАК Номенклатура,
	|			СУММА(Партии.КоличествоОстаток) КАК КоличествоОстатокВсего
	|		ИЗ
	|			Партии КАК Партии
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Партии.Склад,
	|			Партии.Номенклатура) КАК ВложенныйЗапрос1
	|		ПО ДанныеДокумента.Склад = ВложенныйЗапрос1.Склад
	|			И ДанныеДокумента.Номенклатура = ВложенныйЗапрос1.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Кормосмеси КАК Кормосмеси
	|		ПО ДанныеДокумента.Кормосмесь = Кормосмеси.Кормосмесь
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Склад,
	|	ПорядокПартии,
	|	Партии.Партия.МоментВремени" + ПорядокСортировки + "
	|ИТОГИ
	|	МАКСИМУМ(КоличествоДокумент),
	|	МАКСИМУМ(КоличествоОстаток),
	|	МАКСИМУМ(СуммаОстаток),
	|	МАКСИМУМ(ВсегоНеобходимо),
	|	МАКСИМУМ(КоличествоОстатокВсего)
	|ПО
	|	Кормосмесь,
	|	Номенклатура,
	|	Склад,
	|	Партия";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("МетодСписанияСебестоимости", МетодСписанияСебестоимости);
		
	ВыборкаКормосмеси = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Пока ВыборкаКормосмеси.Следующий() Цикл
		СуммаПрихода = 0;
		ВыборкаНоменклатура	= ВыборкаКормосмеси.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.ЕдиницаХраненияОстатков) Тогда
				ОбщегоНазначенияСервер.СообщитьПользователю("Для номенклатуры """ + ВыборкаНоменклатура.НоменклатураПредставление + """ не указана единица хранения остатков");
				Отказ = Истина;
				Продолжить;
			КонецЕсли;
			
			ВыборкаСклад = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСклад.Следующий() Цикл
				если ВыборкаСклад.ВсегоНеобходимо > ВыборкаСклад.КоличествоОстатокВсего И ВыборкаСклад.Номенклатура.Кормосмесь = Ложь тогда
					ОбщегоНазначенияСервер.СообщитьПользователю("Недостаточно " + ВыборкаНоменклатура.НоменклатураНаименование + " """ + ВыборкаНоменклатура.НоменклатураПредставление 
					+ """ на складе """ + ВыборкаСклад.СкладПредставление + """. Дополнительно необходимо: "  
					 + (ВыборкаСклад.ВсегоНеобходимо - ВыборкаСклад.КоличествоОстатокВсего) + " " + ВыборкаНоменклатура.ЕдИзмОстатков + ".");
					Отказ = Истина;
					Продолжить;
				Иначе
					ОсталосьСписать = ВыборкаСклад.ВсегоНеобходимо;
					ОсталосьСписатьПоПолучателю = 0;
					ОсталосьПоТекущейПартии = 0;
					Выборка = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); // Выборка по партиям
					Если НЕ Выборка.Следующий() тогда
						Продолжить;
					Иначе
						ВыборкаПолучатели = Выборка.Выбрать(); // Выборка по складам получателям
						Выборка.Сбросить();
					КонецЕсли;
					
					Пока ОсталосьСписать > 0 цикл
						
						Если Выборка.КоличествоОстаток = Неопределено тогда
							Выборка.Следующий();
							ОсталосьПоТекущейПартии = Выборка.КоличествоОстаток;
							
							ВыборкаПолучатели.Следующий();
						Иначе
							Если ОсталосьПоТекущейПартии = 0 тогда
								Выборка.Следующий();
								
								ОсталосьПоТекущейПартии = Выборка.КоличествоОстаток;
								
							КонецЕсли;
						КонецЕсли;
						КоличествоСписание = Мин(ОсталосьПоТекущейПартии, ОсталосьСписать);
						ОсталосьСписать = ОсталосьСписать - КоличествоСписание;
						ОсталосьПоТекущейПартии = ОсталосьПоТекущейПартии - КоличествоСписание;
						СуммаСписание = КоличествоСписание * (Выборка.СуммаОстаток/Выборка.КоличествоОстаток);
						СуммаПрихода = СуммаПрихода + СуммаСписание;
						// регистр ПартииТоваровНаСкладах Расход
						Движение = Движения.сжсвПартииТоваровНаСкладах.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.Ферма = ВыборкаПолучатели.ФермаПартия;
						Движение.Склад = ВыборкаПолучатели.Склад;
						Движение.Товар = Выборка.Номенклатура;
						Движение.ДокументПоступления = Выборка.Партия;
						Движение.Количество = КоличествоСписание;
						Движение.Сумма = СуммаСписание;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// регистр ПартииТоваровНаСкладах Приход
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	сжсвИзготовлениеКормосмесиКормосмеси.Количество,
		|	сжсвИзготовлениеКормосмесиКормосмеси.Склад,
		|	сжсвИзготовлениеКормосмесиКормосмеси.Склад.Ферма КАК Ферма,
		|	сжсвИзготовлениеКормосмесиКормосмеси.Кормосмесь
		|ИЗ
		|	Документ.ИзготовлениеУдобрения.Кормосмеси КАК сжсвИзготовлениеКормосмесиКормосмеси
		|ГДЕ
		|	сжсвИзготовлениеКормосмесиКормосмеси.Ссылка = &Ссылка
		|	И сжсвИзготовлениеКормосмесиКормосмеси.Кормосмесь = &Кормосмесь";
		
		Запрос.УстановитьПараметр("Кормосмесь", ВыборкаКормосмеси.Кормосмесь);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Движение = Движения.сжсвПартииТоваровНаСкладах.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Ферма = ВыборкаДетальныеЗаписи.Ферма;
			Движение.Склад = ВыборкаДетальныеЗаписи.Склад;
			Движение.Товар = ВыборкаДетальныеЗаписи.Кормосмесь;
			Движение.ДокументПоступления = Ссылка;
			Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
			Движение.Сумма = СуммаПрихода;
		КонецЕсли;
		
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти