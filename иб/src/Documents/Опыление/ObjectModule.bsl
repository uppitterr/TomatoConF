#Область ПроведениеДокумента

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проверка на задвоение строк
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("Свиноматка");
	ДанныеЗадвоены = ОбщегоНазначенияСервер.ПроверитьДокументНаЗадвоениеСтрок(ЭтотОбъект, "Свиноматки", МассивКолонок);
	если ДанныеЗадвоены тогда
		Отказ = истина;
		Возврат;
	КонецЕсли;
	
	ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ);
	
	Движения.сжсвСостоянияСвиноматок.Записывать = истина;
	Движения.сжсвПроизводственныйЦикл.Записывать = истина;
	Движения.сжсвРезультатыОсеменений.Записывать = истина;
	Движения.сжсвИсторияСвиноматок.Записывать = истина;
	
	ОсмотрАвтоматом = Константы.сжсвОсмотрАвтоматом.Получить();
	ВыполнятьПроверку = Константы.сжсвПроверкаПриПроведении.Получить();
	ИнтервалПЦ = Константы.сжсвИнтервалПроизводственногоЦикла.Получить();
	ВестиУчетПоПроверяемым = Константы.сжсвВестиУчетПоПроверяемым.Получить();
	ПереводитьПриОсмотре = Константы.сжсвПереводПриОсмотре.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сжсвОсеменениеСвиноматки.Ссылка,
	               |	сжсвОсеменениеСвиноматки.Свиноматка,
	               |	сжсвОсеменениеСвиноматки.Хряк,
	               |	сжсвОсеменениеСвиноматки.Хряк2,
	               |	сжсвОсеменениеСвиноматки.Хряк3,
	               |	МАКСИМУМ(сжсвОсеменениеСвиноматки.НомерСтроки) КАК НомерСтроки
	               |ПОМЕСТИТЬ ДанныеДокумента
	               |ИЗ
	               |	Документ.Опыление.Свиноматки КАК сжсвОсеменениеСвиноматки
	               |ГДЕ
	               |	сжсвОсеменениеСвиноматки.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	сжсвОсеменениеСвиноматки.Хряк2,
	               |	сжсвОсеменениеСвиноматки.Хряк,
	               |	сжсвОсеменениеСвиноматки.Свиноматка,
	               |	сжсвОсеменениеСвиноматки.Ссылка,
	               |	сжсвОсеменениеСвиноматки.Хряк3
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	сжсвЖивотныеВНаличииОстатки.Ферма,
	               |	сжсвЖивотныеВНаличииОстатки.Сектор,
	               |	сжсвЖивотныеВНаличииОстатки.Станок,
	               |	сжсвЖивотныеВНаличииОстатки.Свиноматка,
	               |	сжсвЖивотныеВНаличииОстатки.Хряк,
	               |	сжсвЖивотныеВНаличииОстатки.ГруппаЖивотных,
	               |	сжсвЖивотныеВНаличииОстатки.ТехнолГруппа,
	               |	сжсвЖивотныеВНаличииОстатки.Группировка,
	               |	сжсвЖивотныеВНаличииОстатки.Партия,
	               |	сжсвЖивотныеВНаличииОстатки.КоличествоОстаток,
	               |	сжсвЖивотныеВНаличииОстатки.МассаОстаток
	               |ПОМЕСТИТЬ ЖивотныеВНаличии
	               |ИЗ
	               |	РегистрНакопления.сжсвЖивотныеВНаличии.Остатки(&МоментВремени, Ферма = &Ферма) КАК сжсвЖивотныеВНаличииОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	сжсвПроизводственныйЦиклСрезПоследних.Период,
	               |	сжсвПроизводственныйЦиклСрезПоследних.Свиноматка,
	               |	сжсвПроизводственныйЦиклСрезПоследних.Событие,
	               |	сжсвПроизводственныйЦиклСрезПоследних.НомерОсеменения,
	               |	сжсвПроизводственныйЦиклСрезПоследних.НомерЦикла,
	               |	сжсвПроизводственныйЦиклСрезПоследних.Хряк,
	               |	сжсвПроизводственныйЦиклСрезПоследних.Осеменатор
	               |ПОМЕСТИТЬ ПроизводственныеЦиклы
	               |ИЗ
	               |	РегистрСведений.сжсвПроизводственныйЦикл.СрезПоследних(
	               |			&МоментВремени,
	               |			Свиноматка В
	               |				(ВЫБРАТЬ
	               |					ДанныеДокумента.Свиноматка
	               |				ИЗ
	               |					ДанныеДокумента)) КАК сжсвПроизводственныйЦиклСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	сжсвПереводИзГруппыВГруппуСвиноматки.Свиноматка КАК Животное,
	               |	сжсвПереводИзГруппыВГруппуСвиноматки.Ссылка
	               |ПОМЕСТИТЬ Переводы
	               |ИЗ
	               |	Документ.сжсвПереводИзГруппыВГруппу.Свиноматки КАК сжсвПереводИзГруппыВГруппуСвиноматки
	               |ГДЕ
	               |	сжсвПереводИзГруппыВГруппуСвиноматки.Ссылка.ДокументОснование = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	сжсвПереводИзГруппыВГруппуХряки.Хряк,
	               |	сжсвПереводИзГруппыВГруппуХряки.Ссылка
	               |ИЗ
	               |	Документ.сжсвПереводИзГруппыВГруппу.Хряки КАК сжсвПереводИзГруппыВГруппуХряки
	               |ГДЕ
	               |	сжсвПереводИзГруппыВГруппуХряки.Ссылка.ДокументОснование = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДокумента.Ссылка,
	               |	ДанныеДокумента.Свиноматка,
	               |	ДанныеДокумента.Хряк,
	               |	ДанныеДокумента.Хряк2,
	               |	ДанныеДокумента.Хряк3,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.Ферма, ЗНАЧЕНИЕ(Справочник.сжсвФермы.ПустаяСсылка)) КАК Ферма,
	               |	ВЫБОР
	               |		КОГДА ЖивотныеВНаличии.Сектор ЕСТЬ NULL 
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.сжсвКорпуса.ПустаяСсылка)
	               |		ИНАЧЕ ЖивотныеВНаличии.Сектор.Владелец
	               |	КОНЕЦ КАК Корпус,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.Сектор, ЗНАЧЕНИЕ(Справочник.сжсвСектора.ПустаяСсылка)) КАК Сектор,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.Станок, ЗНАЧЕНИЕ(Справочник.сжсвСтанки.ПустаяСсылка)) КАК Станок,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.ГруппаЖивотных, ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.ПустаяСсылка)) КАК Группа,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.ТехнолГруппа, ЗНАЧЕНИЕ(Справочник.сжсвТехнолГруппы.пустаяСсылка)) КАК ТехнолГруппа,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.Группировка, ЗНАЧЕНИЕ(Справочник.сжсвГруппировка.ПустаяСсылка)) КАК Группировка,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.МассаОстаток, 0) КАК МассаОстаток,
	               |	ЕСТЬNULL(ЖивотныеВНаличии.КоличествоОстаток, 0) КАК КоличествоОстаток,
	               |	ЕСТЬNULL(сжсвСостоянияСвиноматокСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.сжсвСостоянияСвиноматки.ПустаяСсылка)) КАК Состояние,
	               |	ЕСТЬNULL(ПоследнееОсеменение.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоОсеменения,
	               |	ЕСТЬNULL(ПоследнееОсеменение.Хряк, ЗНАЧЕНИЕ(Справочник.сжсвХряки.ПустаяСсылка)) КАК ХрякПоследнегоОсеменения,
	               |	ЕСТЬNULL(ПоследнееОсеменение.Осеменатор, ЗНАЧЕНИЕ(Справочник.сжсвСотрудники.ПустаяСсылка)) КАК ОсеменаторПоследнегоОсеменения,
	               |	ЕСТЬNULL(ПоследнееОсеменение.НомерЦикла, 0) КАК НомерЦиклаПоследнегоОсеменения,
	               |	ЕСТЬNULL(ПоследнееОсеменение.НомерОсеменения, 0) КАК НомерПоследнегоОсеменения,
	               |	РАЗНОСТЬДАТ(ЕСТЬNULL(ПоследнееОсеменение.Период, ДАТАВРЕМЯ(1, 1, 1)), ДанныеДокумента.Ссылка.Дата, ДЕНЬ) КАК ДнейОтОсеменения,
	               |	ЕСТЬNULL(ПереводыСв.Ссылка, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка)) КАК ДокументСвиноматка,
	               |	ПереводыХр1.Ссылка КАК ДокументХр1,
	               |	ПереводыХр2.Ссылка КАК ДокументХр2,
	               |	ПереводыХр3.Ссылка КАК ДокументХр3,
	               |	ДанныеДокумента.Свиноматка.Представление,
	               |	ДанныеДокумента.НомерСтроки,
	               |	ЕСТЬNULL(ПоследнийЦикл.НомерЦикла, 0) КАК НомерЦиклаПоследнегоСобытия,
	               |	ЕСТЬNULL(ПоследнийЦикл.Событие, ЗНАЧЕНИЕ(Перечисление.сжсвСобытия.ПустаяСсылка)) КАК ПоследнееСобытие,
	               |	РАЗНОСТЬДАТ(ЕСТЬNULL(ПоследнийЦикл.Период, ДАТАВРЕМЯ(1, 1, 1)), ДанныеДокумента.Ссылка.Дата, ДЕНЬ) КАК ДнейОтПоследнегоСобытия
	               |ПОМЕСТИТЬ ДанныеОсеменение
	               |ИЗ
	               |	ДанныеДокумента КАК ДанныеДокумента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЖивотныеВНаличии КАК ЖивотныеВНаличии
	               |		ПО ДанныеДокумента.Свиноматка = ЖивотныеВНаличии.Свиноматка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сжсвСостоянияСвиноматок.СрезПоследних(
	               |				&МоментВремени,
	               |				Свиноматка В
	               |					(ВЫБРАТЬ
	               |						ДанныеДокумента.Свиноматка
	               |					ИЗ
	               |						ДанныеДокумента)) КАК сжсвСостоянияСвиноматокСрезПоследних
	               |		ПО ДанныеДокумента.Свиноматка = сжсвСостоянияСвиноматокСрезПоследних.Свиноматка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПроизводственныеЦиклы КАК ПоследнееОсеменение
	               |		ПО ДанныеДокумента.Свиноматка = ПоследнееОсеменение.Свиноматка
	               |			И (ПоследнееОсеменение.Событие = ЗНАЧЕНИЕ(Перечисление.сжсвСобытия.Осеменение))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Переводы КАК ПереводыСв
	               |		ПО ДанныеДокумента.Свиноматка = ПереводыСв.Животное
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Переводы КАК ПереводыХр1
	               |		ПО ДанныеДокумента.Хряк = ПереводыХр1.Животное
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Переводы КАК ПереводыХр2
	               |		ПО ДанныеДокумента.Хряк = ПереводыХр2.Животное
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Переводы КАК ПереводыХр3
	               |		ПО ДанныеДокумента.Хряк = ПереводыХр3.Животное
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ПроизводственныеЦиклы.Свиноматка КАК Свиноматка,
	               |			МАКСИМУМ(ПроизводственныеЦиклы.Период) КАК Период
	               |		ИЗ
	               |			ПроизводственныеЦиклы КАК ПроизводственныеЦиклы
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ПроизводственныеЦиклы.Свиноматка) КАК ПоследнееСобытие
	               |			ЛЕВОЕ СОЕДИНЕНИЕ ПроизводственныеЦиклы КАК ПоследнийЦикл
	               |			ПО ПоследнееСобытие.Свиноматка = ПоследнийЦикл.Свиноматка
	               |				И ПоследнееСобытие.Период = ПоследнийЦикл.Период
	               |		ПО ДанныеДокумента.Свиноматка = ПоследнееСобытие.Свиноматка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Свиноматка КАК Животное,
	               |	ДанныеОсеменение.Ферма,
	               |	ДанныеОсеменение.Сектор,
	               |	ДанныеОсеменение.Станок,
	               |	ДанныеОсеменение.Группа,
	               |	ДанныеОсеменение.ТехнолГруппа,
	               |	ДанныеОсеменение.Группировка,
	               |	ДанныеОсеменение.МассаОстаток,
	               |	ДанныеОсеменение.КоличествоОстаток,
	               |	ЕСТЬNULL(ДанныеОсеменение.ДокументСвиноматка, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка)) КАК Документ,
	               |	ДанныеОсеменение.Корпус
	               |ПОМЕСТИТЬ ПеревестиИзГруппыВГруппу
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |ГДЕ
	               |	ДанныеОсеменение.Группа = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеСвинки)
	               |	И НЕ ДанныеОсеменение.КоличествоОстаток = 0
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Хряк,
	               |	ЖивотныеВНаличии.Ферма,
	               |	ЖивотныеВНаличии.Сектор,
	               |	ЖивотныеВНаличии.Станок,
	               |	ЖивотныеВНаличии.ГруппаЖивотных,
	               |	ЖивотныеВНаличии.ТехнолГруппа,
	               |	ЖивотныеВНаличии.Группировка,
	               |	ЖивотныеВНаличии.МассаОстаток,
	               |	ЖивотныеВНаличии.КоличествоОстаток,
	               |	ЕСТЬNULL(ДанныеОсеменение.ДокументХр1, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка)),
	               |	ДанныеОсеменение.Корпус
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЖивотныеВНаличии КАК ЖивотныеВНаличии
	               |		ПО ДанныеОсеменение.Хряк = ЖивотныеВНаличии.Хряк
	               |ГДЕ
	               |	НЕ ДанныеОсеменение.Хряк = ЗНАЧЕНИЕ(Справочник.сжсвХряки.ПустаяСсылка)
	               |	И ЖивотныеВНаличии.КоличествоОстаток ЕСТЬ НЕ NULL 
	               |	И ЕСТЬNULL(ЖивотныеВНаличии.ГруппаЖивотных, ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Хряк2,
	               |	ЖивотныеВНаличии.Ферма,
	               |	ЖивотныеВНаличии.Сектор,
	               |	ЖивотныеВНаличии.Станок,
	               |	ЖивотныеВНаличии.ГруппаЖивотных,
	               |	ЖивотныеВНаличии.ТехнолГруппа,
	               |	ЖивотныеВНаличии.Группировка,
	               |	ЖивотныеВНаличии.МассаОстаток,
	               |	ЖивотныеВНаличии.КоличествоОстаток,
	               |	ЕСТЬNULL(ДанныеОсеменение.ДокументХр2, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка)),
	               |	ДанныеОсеменение.Корпус
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЖивотныеВНаличии КАК ЖивотныеВНаличии
	               |		ПО ДанныеОсеменение.Хряк2 = ЖивотныеВНаличии.Хряк
	               |ГДЕ
	               |	НЕ ДанныеОсеменение.Хряк2 = ЗНАЧЕНИЕ(Справочник.сжсвХряки.ПустаяСсылка)
	               |	И ЖивотныеВНаличии.КоличествоОстаток ЕСТЬ НЕ NULL 
	               |	И ЕСТЬNULL(ЖивотныеВНаличии.ГруппаЖивотных, ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Хряк3,
	               |	ЖивотныеВНаличии.Ферма,
	               |	ЖивотныеВНаличии.Сектор,
	               |	ЖивотныеВНаличии.Станок,
	               |	ЖивотныеВНаличии.ГруппаЖивотных,
	               |	ЖивотныеВНаличии.ТехнолГруппа,
	               |	ЖивотныеВНаличии.Группировка,
	               |	ЖивотныеВНаличии.МассаОстаток,
	               |	ЖивотныеВНаличии.КоличествоОстаток,
	               |	ЕСТЬNULL(ДанныеОсеменение.ДокументХр3, ЗНАЧЕНИЕ(Документ.сжсвПереводИзГруппыВГруппу.ПустаяСсылка)),
	               |	ДанныеОсеменение.Корпус
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЖивотныеВНаличии КАК ЖивотныеВНаличии
	               |		ПО ДанныеОсеменение.Хряк3 = ЖивотныеВНаличии.Хряк
	               |ГДЕ
	               |	НЕ ДанныеОсеменение.Хряк3 = ЗНАЧЕНИЕ(Справочник.сжсвХряки.ПустаяСсылка)
	               |	И ЖивотныеВНаличии.КоличествоОстаток ЕСТЬ НЕ NULL 
	               |	И ЕСТЬNULL(ЖивотныеВНаличии.ГруппаЖивотных, ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.сжсвГруппыЖивотных.РемонтныеХрячки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПеревестиИзГруппыВГруппу.Ферма,
	               |	ПеревестиИзГруппыВГруппу.Сектор,
	               |	ПеревестиИзГруппыВГруппу.Группа,
	               |	ПеревестиИзГруппыВГруппу.ТехнолГруппа,
	               |	ПеревестиИзГруппыВГруппу.Группировка,
	               |	ВЫБОР
	               |		КОГДА ПеревестиИзГруппыВГруппу.Животное ССЫЛКА Справочник.сжсвСвиноматки
	               |			ТОГДА ""Свиноматка""
	               |		ИНАЧЕ ""Хряк""
	               |	КОНЕЦ КАК Тип
	               |ИЗ
	               |	ПеревестиИзГруппыВГруппу КАК ПеревестиИзГруппыВГруппу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПеревестиИзГруппыВГруппу.Группа,
	               |	ПеревестиИзГруппыВГруппу.ТехнолГруппа,
	               |	ПеревестиИзГруппыВГруппу.Группировка,
	               |	ПеревестиИзГруппыВГруппу.Сектор,
	               |	ПеревестиИзГруппыВГруппу.Ферма,
	               |	ВЫБОР
	               |		КОГДА ПеревестиИзГруппыВГруппу.Животное ССЫЛКА Справочник.сжсвСвиноматки
	               |			ТОГДА ""Свиноматка""
	               |		ИНАЧЕ ""Хряк""
	               |	КОНЕЦ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Переводы.Ссылка КАК Документ
	               |ИЗ
	               |	Переводы КАК Переводы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Ссылка,
	               |	ДанныеОсеменение.Свиноматка,
	               |	ДанныеОсеменение.Хряк,
	               |	ДанныеОсеменение.Хряк2,
	               |	ДанныеОсеменение.Хряк3,
	               |	ДанныеОсеменение.Ферма,
	               |	ДанныеОсеменение.Корпус,
	               |	ДанныеОсеменение.Сектор,
	               |	ДанныеОсеменение.Станок,
	               |	ДанныеОсеменение.Группа,
	               |	ДанныеОсеменение.ТехнолГруппа,
	               |	ДанныеОсеменение.Группировка,
	               |	ДанныеОсеменение.МассаОстаток,
	               |	ДанныеОсеменение.КоличествоОстаток,
	               |	ДанныеОсеменение.Состояние,
	               |	ДанныеОсеменение.ДатаПоследнегоОсеменения,
	               |	ДанныеОсеменение.ХрякПоследнегоОсеменения,
	               |	ДанныеОсеменение.ОсеменаторПоследнегоОсеменения,
	               |	ДанныеОсеменение.НомерЦиклаПоследнегоОсеменения,
	               |	ДанныеОсеменение.НомерПоследнегоОсеменения,
	               |	ДанныеОсеменение.ДнейОтОсеменения,
	               |	ДанныеОсеменение.ДокументСвиноматка,
	               |	ДанныеОсеменение.ДокументХр1,
	               |	ДанныеОсеменение.ДокументХр2,
	               |	ДанныеОсеменение.ДокументХр3,
	               |	ДанныеОсеменение.СвиноматкаПредставление,
	               |	ДанныеОсеменение.НомерСтроки,
	               |	ДанныеОсеменение.НомерЦиклаПоследнегоСобытия,
	               |	ДанныеОсеменение.ПоследнееСобытие,
	               |	ДанныеОсеменение.ДнейОтПоследнегоСобытия,
	               |	ЕСТЬNULL(сжсвГнездаВНаличииОстатки.КоличествоОстаток, 0) КАК ГнездаОстаток,
	               |	ДанныеОсеменение.Сектор.Тип КАК ТипСектора,
	               |	ДанныеОсеменение.Ферма.УстанавливатьОграничениеПоСекторамОсеменения КАК УстанавливатьОграничениеПоСекторамОсеменения
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.сжсвГнездаВНаличии.Остатки(
	               |				&МоментВремени,
	               |				Свиноматка В
	               |					(ВЫБРАТЬ
	               |						ДанныеДокумента.Свиноматка
	               |					ИЗ
	               |						ДанныеДокумента)) КАК сжсвГнездаВНаличииОстатки
	               |		ПО ДанныеОсеменение.Свиноматка = сжсвГнездаВНаличииОстатки.Свиноматка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПеревестиИзГруппыВГруппу.Животное,
	               |	ПеревестиИзГруппыВГруппу.Ферма,
	               |	ПеревестиИзГруппыВГруппу.Корпус,
	               |	ПеревестиИзГруппыВГруппу.Сектор,
	               |	ПеревестиИзГруппыВГруппу.Станок,
	               |	ПеревестиИзГруппыВГруппу.Группа,
	               |	ПеревестиИзГруппыВГруппу.ТехнолГруппа КАК ТехнолГруппа,
	               |	ПеревестиИзГруппыВГруппу.Группировка,
	               |	ПеревестиИзГруппыВГруппу.МассаОстаток,
	               |	ПеревестиИзГруппыВГруппу.КоличествоОстаток,
	               |	ПеревестиИзГруппыВГруппу.Документ,
	               |	ВЫБОР
	               |		КОГДА ПеревестиИзГруппыВГруппу.Животное ССЫЛКА Справочник.сжсвСвиноматки
	               |			ТОГДА ""Свиноматка""
	               |		ИНАЧЕ ""Хряк""
	               |	КОНЕЦ КАК Тип
	               |ИЗ
	               |	ПеревестиИзГруппыВГруппу КАК ПеревестиИзГруппыВГруппу
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеОсеменение.Свиноматка,
	               |	сжсвРезультатыОсемененийСрезПоследних.Хряк,
	               |	сжсвРезультатыОсемененийСрезПоследних.ДатаОсеменения,
	               |	сжсвРезультатыОсемененийСрезПоследних.Осеменатор,
	               |	сжсвРезультатыОсемененийСрезПоследних.Хряк2,
	               |	сжсвРезультатыОсемененийСрезПоследних.Хряк3,
	               |	ДанныеОсеменение.Группа
	               |ИЗ
	               |	ДанныеОсеменение КАК ДанныеОсеменение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сжсвПроизводственныйЦикл КАК сжсвПроизводственныйЦикл
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сжсвРезультатыОсеменений.СрезПоследних(
	               |					&МоментВремени,
	               |					Свиноматка В
	               |						(ВЫБРАТЬ
	               |							ДанныеДокумента.Свиноматка
	               |						ИЗ
	               |							ДанныеДокумента)) КАК сжсвРезультатыОсемененийСрезПоследних
	               |			ПО сжсвПроизводственныйЦикл.Свиноматка = сжсвРезультатыОсемененийСрезПоследних.Свиноматка
	               |				И сжсвПроизводственныйЦикл.Хряк = сжсвРезультатыОсемененийСрезПоследних.Хряк
	               |				И сжсвПроизводственныйЦикл.Осеменатор = сжсвРезультатыОсемененийСрезПоследних.Осеменатор
	               |				И сжсвПроизводственныйЦикл.Период = сжсвРезультатыОсемененийСрезПоследних.ДатаОсеменения
	               |		ПО ДанныеОсеменение.Свиноматка = сжсвПроизводственныйЦикл.Свиноматка
	               |			И ДанныеОсеменение.НомерЦиклаПоследнегоОсеменения = сжсвПроизводственныйЦикл.НомерЦикла
	               |			И (сжсвПроизводственныйЦикл.Событие = ЗНАЧЕНИЕ(Перечисление.сжсвСобытия.Осеменение))
	               |ГДЕ
	               |	сжсвРезультатыОсемененийСрезПоследних.Свиноматка ЕСТЬ НЕ NULL ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ферма", Ферма);
	РезультатПакет = Запрос.ВыполнитьПакет();
	
	ВыборкаДанныеДокумента = РезультатПакет[8].Выбрать();			// Выборка содержит данные проводимого документа
	ВыборкаПереводИзГруппыВГруппу = РезультатПакет[9].Выбрать();	// Выборка содержит данные для формирования документов "Перевод из группы в группу"
	ВыборкаРазрезыПеревода = РезультатПакет[6].Выбрать();			// Выборка содержит уникальные наборы разрезов учета животных, 
	// 		для формирования документов "Перевод из группы в группу"
	ВыборкаДокументыПеревода = РезультатПакет[7].Выбрать();			// Выборка содержит ссылки на все документы "Перевод из группы в группу",
	// 		созданные на основании текущего документа
	ВыборкаРезультатыОсеменений = РезультатПакет[10].Выбрать();		// Выборка содержит данные для формирования движений в регистре "сжсвРезультатыОсеменений"
	УсловиеПоиска = Новый Структура ("Свиноматка");
	
	// Отмена проведения документов "Перевод из группы в группу", созданных на основании текущего документа.
	пока ВыборкаДокументыПеревода.Следующий() цикл
		ДокументОбъект = ВыборкаДокументыПеревода.Документ.ПолучитьОбъект();
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Исключение
			Отказ = истина;
		КонецПопытки;
	КонецЦикла;		
	
	// Формирование движений
	пока ВыборкаДанныеДокумента.Следующий() цикл
		Выборка = ВыборкаДанныеДокумента;
		если ВыполнятьПроверку тогда
			// Проверка наличия животного на указанной ферме
			если НЕ Выборка.КоличествоОстаток = 1 тогда
				ТекстСообщения = "Свиноматка номер " + Выборка.СвиноматкаПредставление + " не находится на указанной ферме";
				Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
				ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
				Отказ = истина;
				Продолжить;
			иначе
				// Проверка на соответствие состоянию/группе
				если НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.Холостая 
					И НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.УсловноСупоросная
					И НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.Супоросная
					И НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.ПустаяСсылка() тогда
					ТекстСообщения = "Свиноматка  номер " + Выборка.СвиноматкаПредставление + " находится в неподходящем состоянии";
					Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
					ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
					Отказ = истина;
					Продолжить;
				ИначеЕсли НЕ Выборка.Группа = Перечисления.сжсвГруппыЖивотных.РемонтныеСвинки
					И НЕ Выборка.Группа = Перечисления.сжсвГруппыЖивотных.ПроверяемыеСвиноматки
					И НЕ Выборка.Группа = Перечисления.сжсвГруппыЖивотных.ОсновныеСвиноматки тогда
					ТекстСообщения = "Свиноматка  номер " + Выборка.СвиноматкаПредставление + " относится к неподходящей группе";
					Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
					ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
					Отказ = истина;
					Продолжить;
				КонецЕсли;
				// Проверка нахождения животного в соответствующем секторе
				Если Выборка.УстанавливатьОграничениеПоСекторамОсеменения Тогда
					Если ЗначениеЗаполнено(Выборка.Сектор) И НЕ Выборка.ТипСектора = Перечисления.сжсвТипыСекторов.Осеменение Тогда
						ТекстСообщения = "Свиноматка  номер " + Выборка.СвиноматкаПредставление + " находится в неподходящем секторе";
						Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
						ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
						Отказ = истина;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		// регистр СостоянияСвиноматок 
		Движение = Движения.сжсвСостоянияСвиноматок.Добавить();
		Движение.Период = Дата;
		Движение.Свиноматка = Выборка.Свиноматка;
		Движение.Состояние = Перечисления.сжсвСостоянияСвиноматки.УсловноСупоросная;
		
		ДанныеНумерации = ВернутьНомерПроизводственногоЦикла (Выборка, ИнтервалПЦ);
		Если (ОсмотрАвтоматом) И НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.Холостая
			И НЕ Выборка.Состояние = Перечисления.сжсвСостоянияСвиноматки.ПустаяСсылка() Тогда
			
			// регистр ПроизводственныйЦикл 
			Движение = Движения.сжсвПроизводственныйЦикл.Добавить();
			Движение.Период = Дата-1;
			Движение.Свиноматка = Выборка.Свиноматка;
			Движение.Событие = Перечисления.сжсвСобытия.Постановка;
			Движение.ДатаСобытия = Дата-1;
			Движение.НомерЦикла = ДанныеНумерации.НомерЦикла;
			Движение.Ферма = Ферма;
			
			// регистр ИсторияСвиноматок
			Движение = Движения.сжсвИсторияСвиноматок.Добавить();
			Движение.Период = Дата-1;
			Движение.Свиноматка = Выборка.Свиноматка;
			Движение.Событие = Перечисления.сжсвСобытия.Постановка;
			Движение.Документ = Ссылка;
			Движение.ДатаСобытия = Дата-1;
			
			если ЗначениеЗаполнено (Выборка.ДатаПоследнегоОсеменения) 
				И (Выборка.НомерЦиклаПоследнегоОсеменения = Выборка.НомерЦиклаПоследнегоСобытия)
				//И Выборка.ДнейОтОсеменения > 1 тогда
				И ДанныеНумерации.НомерЦиклаИзменился тогда
				Событие = ОбщегоНазначенияСервер.СобытиеХолостойСвиноматки(Выборка.ДнейОтОсеменения, Выборка.Состояние);
				
				если Событие = Перечисления.сжсвСобытия.РаннийОтъем И НЕ Выборка.ГнездаОстаток = 0 тогда
					ТекстСообщения = "У свиноматки  номер " + Выборка.СвиноматкаПредставление + " имеются подсосные поросята на остатке";
					Поле = "Свиноматки[" + (Выборка.НомерСтроки - 1) + "].Свиноматка";
					ОбщегоНазначенияСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
					Отказ = истина;
					Продолжить;
				КонецЕсли;
				
				// регистр ПроизводственныйЦикл 
				Движение = Движения.сжсвПроизводственныйЦикл.Добавить();
				Движение.Период = Дата-2;
				Движение.Свиноматка = Выборка.Свиноматка;
				Движение.Событие = Событие;
				Движение.ДатаСобытия = Дата-2;
				если ДанныеНумерации.НомерЦиклаИзменился тогда
					Движение.НомерЦикла = ДанныеНумерации.НомерЦикла - 1;
				иначе
					Движение.НомерЦикла = ДанныеНумерации.НомерЦикла;
				КонецЕсли;
				Движение.Ферма = Ферма;
				
				// регистр ИсторияСвиноматок
				Движение = Движения.сжсвИсторияСвиноматок.Добавить();
				Движение.Период = Дата-2;
				Движение.Свиноматка = Выборка.Свиноматка;
				Движение.Событие = Событие;
				Движение.Документ = ЭтотОбъект.Ссылка;
				Движение.ДатаСобытия = Дата-2;
				
				УсловиеПоиска.Свиноматка = Выборка.Свиноматка;
				ВыборкаРезультатыОсеменений.Сбросить();
				пока ВыборкаРезультатыОсеменений.НайтиСледующий (УсловиеПоиска) цикл
					// регистр РезультатыОсеменений
					Движение = Движения.сжсвРезультатыОсеменений.Добавить();
					Движение.Период = Дата - 1;
					Движение.Ферма = Ферма;
					Движение.Свиноматка = ВыборкаРезультатыОсеменений.Свиноматка;
					Движение.Хряк = ВыборкаРезультатыОсеменений.Хряк;
					Движение.Хряк2 = ВыборкаРезультатыОсеменений.Хряк2;
					Движение.Хряк3 = ВыборкаРезультатыОсеменений.Хряк3;
					Движение.Осеменатор = ВыборкаРезультатыОсеменений.Осеменатор;
					Движение.ДатаОсеменения = ВыборкаРезультатыОсеменений.ДатаОсеменения;
					Если Выборка.Группа = Перечисления.сжсвГруппыЖивотных.ОсновныеСвиноматки Тогда
						Движение.Основная  = Истина;
					Иначе
						Движение.Основная = Ложь;
					КонецЕсли;
					Если Событие = Перечисления.сжсвСобытия.Перегул Тогда
						Движение.Перегул = 1;
					ИначеЕсли Событие = Перечисления.сжсвСобытия.Прохолост Тогда
						Движение.Прохолост = 1;
					ИначеЕсли Событие = Перечисления.сжсвСобытия.Аборт Тогда
						Движение.Аборт = 1;
					ИначеЕсли Событие = Перечисления.сжсвСобытия.РаннийОтъем Тогда
						Движение.РаннийОтъем = 1;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			ОтразитьПостановку = Ложь;
			Если НЕ ЗначениеЗаполнено (Выборка.ПоследнееСобытие) Тогда
				ОтразитьПостановку = Истина;
			Иначе
				если Выборка.НомерЦиклаПоследнегоОсеменения = Выборка.НомерЦиклаПоследнегоСобытия Тогда
					ОтразитьПостановку = Истина;
				КонецЕсли;
			КонецЕсли;
			Если ОтразитьПостановку Тогда
				// регистр ПроизводственныйЦикл 
				Движение = Движения.сжсвПроизводственныйЦикл.Добавить();
				Движение.Период = Дата-1;
				Движение.Свиноматка = Выборка.Свиноматка;
				Движение.Событие = Перечисления.сжсвСобытия.Постановка;
				Движение.ДатаСобытия = Дата-1;
				Движение.НомерЦикла = ДанныеНумерации.НомерЦикла;
				Движение.Ферма = Ферма;
				
				// регистр ИсторияСвиноматок
				Движение = Движения.сжсвИсторияСвиноматок.Добавить();
				Движение.Период = Дата-1;
				Движение.Свиноматка = Выборка.Свиноматка;
				Движение.Событие = Перечисления.сжсвСобытия.Постановка;
				Движение.Документ = ЭтотОбъект.Ссылка;
				Движение.ДатаСобытия = Дата-1;
			КонецЕсли;
		КонецЕсли;
		
		// регистр ПроизводственныйЦикл 
		Движение = Движения.сжсвПроизводственныйЦикл.Добавить();
		Движение.Период = Дата;
		Движение.Свиноматка = Выборка.Свиноматка;
		Движение.Событие = Перечисления.сжсвСобытия.Осеменение;
		Движение.ДатаСобытия = Дата;
		Движение.Хряк = Выборка.Хряк;
		Движение.Хряк2 = Выборка.Хряк2;
		Движение.Хряк3 = Выборка.Хряк3;
		Движение.Осеменатор = Ответственный;
		Движение.НомерОсеменения = ДанныеНумерации.НомерСобытия;
		Движение.НомерЦикла = ДанныеНумерации.НомерЦикла;
		Движение.ИскусственноеОсеменение = ИскусственноеОсеменение;		
		Движение.Ферма = Ферма;
		
		// регистр РезультатыОсеменений
		Движение = Движения.сжсвРезультатыОсеменений.Добавить();
		Движение.Период = Дата;
		Движение.Ферма = Ферма;
		Движение.Свиноматка = Выборка.Свиноматка;
		Движение.Хряк = Выборка.Хряк;
		Движение.Хряк2 = Выборка.Хряк2;
		Движение.Хряк3 = Выборка.Хряк3;
		Движение.Осеменатор = Ответственный;
		Движение.ДатаОсеменения = Дата;
		Если Выборка.Группа = Перечисления.сжсвГруппыЖивотных.ОсновныеСвиноматки Тогда
			Движение.Основная  = Истина;
		Иначе
			Движение.Основная = Ложь;
		КонецЕсли;
		Движение.Осеменение = 1;
		Движение.ИскусственноеОсеменение = ИскусственноеОсеменение;		
		
		// регистр ИсторияСвиноматок
		Движение = Движения.сжсвИсторияСвиноматок.Добавить();
		Движение.Период = Дата;
		Движение.Свиноматка = Выборка.Свиноматка;
		Движение.Событие = Перечисления.сжсвСобытия.Осеменение;
		Движение.Документ = ЭтотОбъект.Ссылка;
		Движение.ДатаСобытия = Дата;
	КонецЦикла;
	
	ДатаПеревода = Дата+1;
	МассаСвиноматокПроверяемые = Константы.сжсвМассаСвиноматокПриПереводеВПроверяемые.Получить();
	МассаХряковПроверяемые = Константы.сжсвМассаХряковПриПереводеВПроверяемые.Получить();
	МассаСвиноматокОсновные = Константы.сжсвМассаСвиноматокПриПереводеВОсновные.Получить();
	МассаХряковОсновные = Константы.сжсвМассаХряковПриПереводеВОсновные.Получить();
	МассаСвиноматок = ?(ВестиУчетПоПроверяемым,МассаСвиноматокПроверяемые,МассаСвиноматокОсновные);
	МассаХряков = ?(ВестиУчетПоПроверяемым,МассаХряковПроверяемые,МассаХряковОсновные);

	// Перевод ремонтного поголовья в проверяемые
	пока ВыборкаРазрезыПеревода.Следующий() цикл
		УсловиеПоиска = Новый Структура;
		УсловиеПоиска.Вставить("Ферма", ВыборкаРазрезыПеревода.Ферма);
		УсловиеПоиска.Вставить("Сектор", ВыборкаРазрезыПеревода.Сектор);
		УсловиеПоиска.Вставить("Группа", ВыборкаРазрезыПеревода.Группа);
		УсловиеПоиска.Вставить("ТехнолГруппа", ВыборкаРазрезыПеревода.ТехнолГруппа);
		УсловиеПоиска.Вставить("Группировка", ВыборкаРазрезыПеревода.Группировка);
		УсловиеПоиска.Вставить("Тип", ВыборкаРазрезыПеревода.Тип);
		
		ПереводОбъект = Неопределено;
		ВыборкаПереводИзГруппыВГруппу.Сбросить();
		пока ВыборкаПереводИзГруппыВГруппу.НайтиСледующий(УсловиеПоиска) цикл
			если ПереводОбъект = Неопределено тогда
				если ЗначениеЗаполнено (ВыборкаПереводИзГруппыВГруппу.Документ) тогда
					Если НЕ ПереводитьПриОсмотре И ТипЗнч(ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвСвиноматки") Тогда
						ВидДействия = "Перезаполнен ";
						ПереводОбъект = ВыборкаПереводИзГруппыВГруппу.Документ.ПолучитьОбъект();
						ПереводОбъект.Свиноматки.Очистить();
						ПереводОбъект.Хряки.Очистить();
						ПереводОбъект.Дата = ДатаПеревода;
						ПереводОбъект.Ферма = Ферма;
						ПереводОбъект.СформированАвтоматически = Истина;
						ПереводОбъект.Корпус = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.КорпусКуда = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.Сектор = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.СекторКуда = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.Группировка = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ГруппировкаКуда = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ТехнолГруппа = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.ТехнолГруппаКуда = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.Группа = ВыборкаПереводИзГруппыВГруппу.Группа;
						ПереводОбъект.ГруппаКуда = ?(ВестиУчетПоПроверяемым, Перечисления.сжсвГруппыЖивотных.ПроверяемыеСвиноматки, Перечисления.сжсвГруппыЖивотных.ОсновныеСвиноматки);
						ПереводОбъект.ДокументОснование = Ссылка;
					ИначеЕсли ТипЗнч(ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвХряки") Тогда
						ВидДействия = "Перезаполнен ";
						ПереводОбъект = ВыборкаПереводИзГруппыВГруппу.Документ.ПолучитьОбъект();
						ПереводОбъект.Свиноматки.Очистить();
						ПереводОбъект.Хряки.Очистить();
						ПереводОбъект.Дата = ДатаПеревода;
						ПереводОбъект.Ферма = Ферма;
						ПереводОбъект.СформированАвтоматически = Истина;
						ПереводОбъект.Корпус = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.КорпусКуда = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.Сектор = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.СекторКуда = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.Группировка = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ГруппировкаКуда = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ТехнолГруппа = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.ТехнолГруппаКуда = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.Группа = ВыборкаПереводИзГруппыВГруппу.Группа;
						ПереводОбъект.ГруппаКуда = ?(ВестиУчетПоПроверяемым, Перечисления.сжсвГруппыЖивотных.ПроверяемыеХряки, Перечисления.сжсвГруппыЖивотных.Хряки);
						ПереводОбъект.ДокументОснование = Ссылка;   
					КонецЕсли;
				иначе
					Если НЕ ПереводитьПриОсмотре И ТипЗнч(ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвСвиноматки") Тогда
						ВидДействия = "Создан ";
						ПереводОбъект = Документы.сжсвПереводИзГруппыВГруппу.СоздатьДокумент();
						ПереводОбъект.Дата = ДатаПеревода;
						ПереводОбъект.Ферма = Ферма;
						ПереводОбъект.СформированАвтоматически = Истина;
						ПереводОбъект.Корпус = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.КорпусКуда = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.Сектор = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.СекторКуда = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.Группировка = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ГруппировкаКуда = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ТехнолГруппа = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.ТехнолГруппаКуда = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.Группа = ВыборкаПереводИзГруппыВГруппу.Группа;
						ПереводОбъект.ГруппаКуда = ?(ВестиУчетПоПроверяемым, Перечисления.сжсвГруппыЖивотных.ПроверяемыеСвиноматки, Перечисления.сжсвГруппыЖивотных.ОсновныеСвиноматки);
						ПереводОбъект.ДокументОснование = Ссылка;
					ИначеЕсли ТипЗнч(ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвХряки") Тогда
						ВидДействия = "Создан ";
						ПереводОбъект = Документы.сжсвПереводИзГруппыВГруппу.СоздатьДокумент();
						ПереводОбъект.Дата = ДатаПеревода;
						ПереводОбъект.Ферма = Ферма;
						ПереводОбъект.СформированАвтоматически = Истина;
						ПереводОбъект.Корпус = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.КорпусКуда = ВыборкаПереводИзГруппыВГруппу.Корпус;
						ПереводОбъект.Сектор = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.СекторКуда = ВыборкаПереводИзГруппыВГруппу.Сектор;
						ПереводОбъект.Группировка = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ГруппировкаКуда = ВыборкаПереводИзГруппыВГруппу.Группировка;
						ПереводОбъект.ТехнолГруппа = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.ТехнолГруппаКуда = ВыборкаПереводИзГруппыВГруппу.ТехнолГруппа;
						ПереводОбъект.Группа = ВыборкаПереводИзГруппыВГруппу.Группа;
						ПереводОбъект.ГруппаКуда = ?(ВестиУчетПоПроверяемым, Перечисления.сжсвГруппыЖивотных.ПроверяемыеХряки, Перечисления.сжсвГруппыЖивотных.Хряки) ;
						ПереводОбъект.ДокументОснование = Ссылка;
					КонецЕсли;
				КонецЕсли;
				ДатаПеревода = ДатаПеревода + 1;
			КонецЕсли;
			
			если ТипЗнч (ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвСвиноматки") И  НЕ ПереводитьПриОсмотре тогда
				НоваяСтрока = ПереводОбъект.Свиноматки.Добавить();
				НоваяСтрока.Свиноматка = ВыборкаПереводИзГруппыВГруппу.Животное;
				НоваяСтрока.Масса = ?(МассаСвиноматок = 0, ВыборкаПереводИзГруппыВГруппу.МассаОстаток, МассаСвиноматок);
				НоваяСтрока.Станок = ВыборкаПереводИзГруппыВГруппу.Станок;
			иначеЕсли  ТипЗнч(ВыборкаПереводИзГруппыВГруппу.Животное) = Тип ("СправочникСсылка.сжсвХряки") Тогда
				НоваяСтрока = ПереводОбъект.Хряки.Добавить();
				НоваяСтрока.Хряк = ВыборкаПереводИзГруппыВГруппу.Животное;
				НоваяСтрока.Масса = ?(МассаХряков = 0, ВыборкаПереводИзГруппыВГруппу.МассаОстаток, МассаХряков);
				НоваяСтрока.Станок = ВыборкаПереводИзГруппыВГруппу.Станок;
			КонецЕсли;
		КонецЦикла;
		
		если НЕ ПереводОбъект = Неопределено тогда
			ПереводОбъект.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначенияСервер.СообщитьПользователю(ВидДействия + "документ " + ПереводОбъект, ПереводОбъект);
			Попытка
				ПереводОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначенияСервер.СообщитьПользователю("При попытке проведения возникла ошибка: " + ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВернутьНомерПроизводственногоЦикла (Выборка, ИнтервалПЦ)
	
	НомерСобытия = 0;
	НомерЦикла = 0;
	НомерЦиклаИзменился = ложь;
	
	если НЕ ЗначениеЗаполнено(Выборка.ПоследнееСобытие) тогда
		НомерСобытия = 1;
		НомерЦикла = 1;
	иначе
		если Выборка.ПоследнееСобытие = Перечисления.сжсвСобытия.Осеменение тогда
			если Выборка.ДнейОтПоследнегоСобытия <= ИнтервалПЦ тогда
				НомерСобытия = Выборка.НомерПоследнегоОсеменения + 1;
				НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия;
			иначе
				НомерСобытия = 1;
				НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия + 1;
				НомерЦиклаИзменился = истина;
			КонецЕсли;
		иначеЕсли Выборка.ПоследнееСобытие = Перечисления.сжсвСобытия.Постановка тогда
			если Выборка.ДнейОтПоследнегоСобытия <= ИнтервалПЦ тогда
				если Выборка.НомерЦиклаПоследнегоОсеменения = Выборка.НомерЦиклаПоследнегоСобытия тогда
					НомерСобытия = Выборка.НомерПоследнегоОсеменения + 1;
					НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия;
				иначе
					НомерСобытия = 1;
					НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия;
				КонецЕсли;
			иначе
				НомерСобытия = 1;
				НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия + 1;
				НомерЦиклаИзменился = истина;
			КонецЕсли;		
		иначеЕсли Выборка.ПоследнееСобытие = Перечисления.сжсвСобытия.Охота тогда
			если Выборка.НомерЦиклаПоследнегоОсеменения = Выборка.НомерЦиклаПоследнегоСобытия тогда
				НомерСобытия = Выборка.НомерПоследнегоОсеменения + 1;
				НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия;
			иначе
				НомерСобытия = 1;
				НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия;
			КонецЕсли;
		иначе
			НомерСобытия = 1;
			НомерЦикла = Выборка.НомерЦиклаПоследнегоСобытия + 1;
			НомерЦиклаИзменился = истина;
		КонецЕсли;
	КонецЕсли;
	
	если НомерСобытия = 0 тогда
		НомерСобытия = 1;
	КонецЕсли;
	если НомерЦикла = 0 тогда
		НомерЦикла = 1;
	КонецЕсли;
	
	Возврат (Новый Структура("НомерСобытия, НомерЦикла, НомерЦиклаИзменился", НомерСобытия, НомерЦикла, НомерЦиклаИзменился));
	
КонецФункции

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ);
	
КонецПроцедуры

Процедура ОтменитьПроведениеВведенныхНаОснованииДокументов(Отказ)	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	сжсвПереводИзГруппыВГруппу.Ссылка
	|ИЗ
	|	Документ.сжсвПереводИзГруппыВГруппу КАК сжсвПереводИзГруппыВГруппу
	|ГДЕ
	|	сжсвПереводИзГруппыВГруппу.ДокументОснование = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	пока Выборка.Следующий() цикл
		Попытка
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Записать (РежимЗаписиДокумента.ОтменаПроведения);
		Исключение
			Отказ = истина;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти