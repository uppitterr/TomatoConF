// Структура хранения базы данных v1.2
// (с) Коротаев Никита, 2012
// Infostart (http://infostart.ru/public/147147/)


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДеревоОбъектовМетаданныхЗаполнить();
	ИмяКонфигурации = Метаданные.Имя;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ТипСтруктуры = 2 Тогда
		ТипСтруктуры = 1;
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПриАктивизацииСтроки(Элемент)
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.ПолноеИмя) Тогда
		ЗаполнитьТаблицыПоОбъекту();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные.Имя = ИмяКонфигурации Тогда
		ЗаполнитьТаблицыПоОбъекту(Истина);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыПриАктивизацииСтроки(Элемент)
	
	Если Не Элемент.ТекущиеДанные = Неопределено Тогда
		ЗаполнитьСписокПолейИндексов();
	Иначе
		Поля.Очистить();
		Индексы.Очистить();
		ПоляИндексов.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексыПриАктивизацииСтроки(Элемент)
	
	Если Не Элемент.ТекущиеДанные = Неопределено Тогда
		ЗаполнитьПоляИндексовНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСтруктурыПриИзменении(Элемент)
	
	ЗаполнитьТаблицыПоОбъекту();
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ

&НаКлиенте
Процедура ЗаполнитьТаблицыПоОбъекту(ПолныйСписок = Ложь)
	
	ТекДанные = Элементы.ДеревоОбъектовМетаданных.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекДанные.ПолноеИмя) Или ПолныйСписок Тогда
		ЗаполнитьТаблицыПоОбъектуНаСервере(ТекДанные.ПолноеИмя, Не ЗначениеЗаполнено(ТекДанные.ПолноеИмя));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыПоОбъектуНаСервере(знач МетаОбъект, ВидимостьКолонкиМетаданных)
	
	ОбъектыМетаданных = Новый Массив;
	// Патч для некорректно работающих регламентных заданий
	Если Найти(МетаОбъект, "РегламентноеЗадание.") > 0 Тогда
		МетаОбъект = Метаданные.НайтиПоПолномуИмени(МетаОбъект);
	КонецЕсли;
	// Конец патча
	ОбъектыМетаданных.Добавить(МетаОбъект);
	СтруктураДанных = ПолучитьСтруктуруХраненияБазыДанных(ОбъектыМетаданных, ТипСтруктуры = 1);

	Таблицы.Очистить();
	Для Каждого СтрокаСтруктуры Из СтруктураДанных Цикл
		НоваяЗапись = Таблицы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаСтруктуры,,"Поля,Индексы");
		НоваяЗапись.Поля.Загрузить(СтрокаСтруктуры.Поля);
		
		Для Каждого Индекс Из СтрокаСтруктуры.Индексы Цикл
			НовыйИднекс 					= НоваяЗапись.Индексы.Добавить();
			НовыйИднекс.ИмяИндексаХранения 	= Индекс.ИмяИндексаХранения;
			НовыйИднекс.Поля.Загрузить(Индекс.Поля);
		КонецЦикла;
		
	КонецЦикла;
	Элементы.ТаблицыМетаданные.Видимость = ВидимостьКолонкиМетаданных;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокПолейИндексов()

	ТекДанные = Элементы.Таблицы.ТекущиеДанные;
	ЗаполнитьСписокПолейИндексовНаСервере(ТекДанные.ПолучитьИдентификатор());
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПолейИндексовНаСервере(Идентификатор)
	
	Строка = Таблицы.НайтиПоИдентификатору(Идентификатор);
	Поля.Загрузить(Строка.Поля.Выгрузить());
	Индексы.Загрузить(Строка.Индексы.Выгрузить());

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоляИндексовНаКлиенте()

	ТекТаблица = Элементы.Таблицы.ТекущиеДанные;
	ТекИндексы = Элементы.Индексы.ТекущиеДанные;
	СтруктураПоиска = Новый Структура("ИмяИндексаХранения", ТекИндексы.ИмяИндексаХранения);
	НайденныеСтроки = ТекТаблица.Индексы.НайтиСтроки(СтруктураПоиска);

	Если НайденныеСтроки.Количество() = 1 Тогда
		ПоляИндексов.Очистить();
		СписокПолей = НайденныеСтроки[0].Поля;
		Для Каждого ПолеИндекса Из СписокПолей Цикл
			НовоеПолеИндекса = ПоляИндексов.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеПолеИндекса, ПолеИндекса);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляИндексовНаСервере(Идентификатор)

	Строка = Индексы.НайтиПоИдентификатору(Идентификатор);
	ПоляИндексов.Загрузить(Строка.Поля.Выгрузить());
	
КонецПроцедуры // ЗаполнитьПоляИндексовНаСервере()



////////////////////////////////////////////////////////////////////////////////
// ЗАПОЛНЕНИЕ ДЕРЕВА МЕТАДАННЫХ

// Процедура заполняет дерево значений объектов конфигурации.
//
&НаСервере
Процедура ДеревоОбъектовМетаданныхЗаполнить()
	
	КоллекцииОбъектовМетаданных = Новый ТаблицаЗначений;
	КоллекцииОбъектовМетаданных.Колонки.Добавить("Имя");
	КоллекцииОбъектовМетаданных.Колонки.Добавить("ПолноеИмя");
	КоллекцииОбъектовМетаданных.Колонки.Добавить("Синоним");
	КоллекцииОбъектовМетаданных.Колонки.Добавить("Картинка");
	КоллекцииОбъектовМетаданных.Колонки.Добавить("КартинкаОбъекта");
	КоллекцииОбъектовМетаданных.Колонки.Добавить("ЭтоКоллекцияОбщие");
	
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Подсистемы",                   "Подсистемы",                     35, 36, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ОбщиеМодули",                  "Общие модули",                   37, 38, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ПараметрыСеанса",              "Параметры сеанса",               39, 40, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Роли",                         "Роли",                           41, 42, Истина, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("ПланыОбмена",                  "Планы обмена",                  БиблиотекаКартинок.ПланОбмена, 			БиблиотекаКартинок.ПланОбмена, 					Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("КритерииОтбора",               "Критерии отбора",                45, 46, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ПодпискиНаСобытия",            "Подписки на события",            47, 48, Истина, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("РегламентныеЗадания",          "Регламентные задания",          БиблиотекаКартинок.РегламентноеЗадание, 	БиблиотекаКартинок.РегламентноеЗадание, 		Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ФункциональныеОпции",          "Функциональные опции",           51, 52, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ПараметрыФункциональныхОпций", "Параметры функциональных опций", 53, 54, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ХранилищаНастроек",            "Хранилища настроек",            БиблиотекаКартинок.ХранилищеНастроек, 		БиблиотекаКартинок.ХранилищеНастроек, 			Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ОбщиеФормы",                   "Общие формы",                    57, 58, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ОбщиеКоманды",                 "Общие команды",                  59, 60, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ГруппыКоманд",                 "Группы команд",                  61, 62, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Интерфейсы",                   "Интерфейсы",                     63, 64, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ОбщиеМакеты",                  "Общие макеты",                   65, 66, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ОбщиеКартинки",                "Общие картинки",                 67, 68, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("ПакетыXDTO",                   "XDTO-пакеты",                    69, 70, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("WebСервисы",                   "Web-сервисы",                    71, 72, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("WSСсылки",                     "WS-ссылки",                      73, 74, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Стили",                        "Стили",                          75, 76, Истина, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Языки",                        "Языки",                          77, 78, Истина, КоллекцииОбъектовМетаданных);
	
	КоллекцииОбъектовМетаданных_НоваяСтрока("Константы",                    "Константы",                     БиблиотекаКартинок.Константа,              БиблиотекаКартинок.Константа,                    Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("Справочники",                  "Справочники",                   БиблиотекаКартинок.Справочник,             БиблиотекаКартинок.Справочник,                   Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("Документы",                    "Документы",                     БиблиотекаКартинок.Документ,               БиблиотекаКартинок.ДокументОбъект,               Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("ЖурналыДокументов",            "Журналы документов",            БиблиотекаКартинок.ЖурналДокументов,       БиблиотекаКартинок.ЖурналДокументов,             Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("Перечисления",                 "Перечисления",                  БиблиотекаКартинок.Перечисление,           БиблиотекаКартинок.Перечисление,                 Ложь, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Отчеты",                       "Отчеты",                        БиблиотекаКартинок.Отчет,                  БиблиотекаКартинок.Отчет,                        Ложь, КоллекцииОбъектовМетаданных);
	//КоллекцииОбъектовМетаданных_НоваяСтрока("Обработки",                    "Обработки",                     БиблиотекаКартинок.Обработка,              БиблиотекаКартинок.Обработка,                    Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("ПланыВидовХарактеристик",      "Планы видов характеристик",     БиблиотекаКартинок.ПланВидовХарактеристик, БиблиотекаКартинок.ПланВидовХарактеристикОбъект, Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("ПланыСчетов",                  "Планы счетов",                  БиблиотекаКартинок.ПланСчетов,             БиблиотекаКартинок.ПланСчетовОбъект,             Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("ПланыВидовРасчета",            "Планы видов расчета",     		 БиблиотекаКартинок.ПланВидовРасчета, 		БиблиотекаКартинок.ПланВидовРасчета, 			 Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("РегистрыСведений",             "Регистры сведений",             БиблиотекаКартинок.РегистрСведений,        БиблиотекаКартинок.РегистрСведений,              Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("РегистрыНакопления",           "Регистры накопления",           БиблиотекаКартинок.РегистрНакопления,      БиблиотекаКартинок.РегистрНакопления,            Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("РегистрыБухгалтерии",          "Регистры бухгалтерии",          БиблиотекаКартинок.РегистрБухгалтерии,     БиблиотекаКартинок.РегистрБухгалтерии,           Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("РегистрыРасчета",              "Регистры расчета",              БиблиотекаКартинок.РегистрРасчета,         БиблиотекаКартинок.РегистрРасчета,               Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("БизнесПроцессы",               "Бизнес-процессы",               БиблиотекаКартинок.БизнесПроцесс,          БиблиотекаКартинок.БизнесПроцессОбъект,          Ложь, КоллекцииОбъектовМетаданных);
	КоллекцииОбъектовМетаданных_НоваяСтрока("Задачи",                       "Задачи",                        БиблиотекаКартинок.Задача,                 БиблиотекаКартинок.ЗадачаОбъект,                 Ложь, КоллекцииОбъектовМетаданных);
	
	// Создание предопределенных элементов.
	ЭлементКонфигурация = НоваяСтрокаДерева(Метаданные.Имя, "", Метаданные.Синоним, 79, ДеревоОбъектовМетаданных);
	ЭлементОбщие        = НоваяСтрокаДерева("Общие",        "", "Общие",             0, ЭлементКонфигурация);
	
	// Заполнение дерева объектов метаданных.
	Для каждого Строка ИЗ КоллекцииОбъектовМетаданных Цикл

		ВыводКоллекцииОбъектовМетаданных(Строка.Имя,
											"",
											Строка.Синоним,
											Строка.Картинка,
											Строка.КартинкаОбъекта,
											?(Строка.ЭтоКоллекцияОбщие, ЭлементОбщие, ЭлементКонфигурация),
											?(Строка.Имя = "Подсистемы", Метаданные.Подсистемы, Неопределено));

	КонецЦикла;
	
	Если ЭлементОбщие.ПолучитьЭлементы().Количество() = 0 Тогда
		ЭлементКонфигурация.ПолучитьЭлементы().Удалить(ЭлементОбщие);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет новую строку в таблицу значений видов объектов метаданных
// конфигурации
//
// Параметры:
// Имя           - имя объекта метаданных или вида объекта метаданных
// Синоним       - синоним объекта метаданных
// Картинка      - картинка поставленная в соответствие объекту метаданных
//                 или виду объекта метаданных
// ЭтоКоллекцияОбщие - признак того, что текущий элемент содержит подэлементы
//
&НаСервере
Процедура КоллекцииОбъектовМетаданных_НоваяСтрока(Имя, Синоним, Картинка, КартинкаОбъекта, ЭтоКоллекцияОбщие, Таб)
	
	НоваяСтрока = Таб.Добавить();
	НоваяСтрока.Имя               = Имя;
	НоваяСтрока.Синоним           = Синоним;
	НоваяСтрока.Картинка          = Картинка;
	НоваяСтрока.КартинкаОбъекта   = КартинкаОбъекта;
	НоваяСтрока.ЭтоКоллекцияОбщие = ЭтоКоллекцияОбщие;
	
КонецПроцедуры

// Добавляет новую строку в дерево формы
// Имя           - имя элемента
// Синоним       - синоним элемента
// Картинка      - код картинки
// Родитель      - элемент дерева значений формы, от которого отращивается новая ветка
//
// Возвращаемое значение:
//  ДанныеФормыЭлементДерево - отрощенная ветвь дерева
//
&НаСервере
Функция НоваяСтрокаДерева(Имя, ПолноеИмя, Синоним, Картинка, Родитель, ЭтоОбъектМетаданных = Ложь)
	
	Коллекция = Родитель.ПолучитьЭлементы();
	НоваяСтрока = Коллекция.Добавить();
	НоваяСтрока.Имя                 = Имя;
	НоваяСтрока.Представление       = ?(ЗначениеЗаполнено(Синоним), Синоним, Имя);
	НоваяСтрока.Пометка             = 0;
	НоваяСтрока.Картинка            = Картинка;
	НоваяСтрока.ПолноеИмя           = ПолноеИмя;
	НоваяСтрока.ЭтоОбъектМетаданных = ЭтоОбъектМетаданных;
	
	Возврат НоваяСтрока;
	
КонецФункции

// Функция, добавляющая одну строку в дерево значений формы - дерево,
// а так же заполняющая полный набор строк из метаданных по переданному параметру
// Если параметр Подсистемы заполнен, то вызывается рекурсивно из за того,
// что подсистемы могут содержать подсистемы
// Парметры:
// Имя           - имя родительского элемента
// Синоним       - синоним родительского элемента
// Пометка       - Булево, начальная пометка коллекции или объекта метаданных.
// Картинка      - код картинки родительского элемента
// КартинкаОбъекта - код картинки подэлемента
// Родитель      - ссылка на элемента дерева значений который является корнем
//                 для добавляемого элемента
// Подсистемы    - если заполнен то содержит значение Метаданные.Подсистемы
//                 т.е. коллекцию элементов
// Проверять     - булево, признак проверки на принадлежность родительским подсистемам
// 
// Возвращаемое значение:
// 
//
&НаСервере
Функция ВыводКоллекцииОбъектовМетаданных(Имя, ПолноеИмя, Синоним, Картинка, КартинкаОбъекта, Родитель = Неопределено, Подсистемы = Неопределено, Проверять = Истина)

	НоваяСтрока = НоваяСтрокаДерева(Имя, ПолноеИмя, Синоним, Картинка, Родитель, Подсистемы <> Неопределено И Подсистемы <> Метаданные.Подсистемы);
	
	Если Подсистемы = Неопределено Тогда
		Для каждого ЭлементКоллекцииМетаданных ИЗ Метаданные[Имя] Цикл

			НоваяСтрокаДерева(ЭлементКоллекцииМетаданных.Имя,
							ЭлементКоллекцииМетаданных.ПолноеИмя(),
							ЭлементКоллекцииМетаданных.Синоним,
							КартинкаОбъекта,
							НоваяСтрока,
							Истина);
		КонецЦикла;
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

