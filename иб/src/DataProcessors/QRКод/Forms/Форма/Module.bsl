
&НаСервере
Процедура СформироватьQRКодНаСервере()
	
	Если Объект.ТипТекста = "Любой текст" Тогда
		СтрокаВФорматеQRКода = Объект.ТекстQRКода;	
	ИначеЕсли Объект.ТипТекста = "Ссылка" Тогда
		СтрокаВФорматеQRКода = СокрЛП(Объект.АдресСайта);
	ИначеЕсли Объект.ТипТекста = "Визитная карточка" Тогда
		СтрокаВФорматеQRКода =  "BEGIN:VCARD" + Символы.ПС + 
								"VERSION:3.0" + Символы.ПС +
								"FN:" + СокрЛП(Объект.Имя) + " " + СокрЛП(Объект.Фамилия) + Символы.ПС +
								"N:" + СокрЛП(Объект.Фамилия) + ";" + СокрЛП(Объект.Имя) + Символы.ПС +
								"TITLE:" + СокрЛП(Объект.Должность) + Символы.ПС +
								"ORG:" + СокрЛП(Объект.Организация) + Символы.ПС +
								"ADR;TYPE=intl, postal, parcel, work:;;" + СокрЛП(Объект.Адрес) + ";" + СокрЛП(Объект.Город) + ";;;" + СокрЛП(Объект.Страна) + Символы.ПС +
								"TEL;TYPE=home:" + СокрЛП(Объект.НомерТелефона) + Символы.ПС +
								"TEL;TYPE=work:" + СокрЛП(Объект.НомерТелефонаРабочий) + Символы.ПС +
								"TEL;TYPE=cell:" + СокрЛП(Объект.НомерТелефонаМобильный) + Символы.ПС +
								"TEL;TYPE=fax:" + СокрЛП(Объект.НомерФакса) + Символы.ПС +
								"EMAIL;TYPE=INTERNET:" + СокрЛП(Объект.АдресПочты) + Символы.ПС +
								"URL:" + СокрЛП(Объект.АдресСайта) + Символы.ПС +
								"BDAY:" + Формат(Объект.ДеньРождения,"ДФ=""гггг-ММ-дд""") + Символы.ПС +
								"END:VCARD";
	ИначеЕсли Объект.ТипТекста = "Телефон" Тогда
		СтрокаВФорматеQRКода = "tel:" + СокрЛП(Объект.НомерТелефона);
	ИначеЕсли Объект.ТипТекста = "SMS" Тогда
		СтрокаВФорматеQRКода = "smsto:" + СокрЛП(Объект.НомерТелефона) + ":" + СокрЛП(Объект.ТекстQRКода);
	ИначеЕсли Объект.ТипТекста = "MMS" Тогда
		СтрокаВФорматеQRКода = "mmsto:" + СокрЛП(Объект.НомерТелефона) + ":" + СокрЛП(Объект.ТекстQRКода);
	ИначеЕсли Объект.ТипТекста = "E-mail" Тогда
		СтрокаВФорматеQRКода = "mailto:" + СокрЛП(Объект.АдресПочты); 
	ИначеЕсли Объект.ТипТекста = "E-mail с текстом" Тогда
		СтрокаВФорматеQRКода =  "MATMSG:" + Символы.ПС + 
								"TO: " + СокрЛП(Объект.АдресПочты) + ";" + Символы.ПС +
								"SUB:" + Объект.ТемаПисьма + ";" + Символы.ПС +
								"BODY:" + Объект.ТекстQRКода + ";" + Символы.ПС +
								";"; 
	ИначеЕсли Объект.ТипТекста = "Географическая информация" Тогда
		СтрокаВФорматеQRКода = "geo:" + СтрЗаменить(Объект.ТекстQRКода," ",""); 
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(СтрокаВФорматеQRКода) Тогда
		
		ОбъектВЗначении = РеквизитФормыВЗначение("Объект");
		
		ДанныеQRКода = ОбъектВЗначении.ДанныеQRКода(СтрокаВФорматеQRКода, 1, 300);
		
		Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
			
			ТекстСообщения = Нстр("ru = 'Не удалось сформировать QR-код'");
			
			ОбъектВЗначении.СообщитьПользователю(ТекстСообщения); 			
			
			Возврат;
			
		КонецЕсли;
		
		// Помещаем двоичные данные из реквизита во временное хранилище. 
		//Адрес из хранилища помещаем в строковой реквизит формы "КартинкаQRКода"
		Объект.КартинкаQRКода = ПоместитьВоВременноеХранилище(Новый Картинка(ДанныеQRКода),УникальныйИдентификатор);
				
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьQRКод(Команда)
	
	СформироватьQRКодНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ТипТекста = "Любой текст";
	
КонецПроцедуры

&НаКлиенте
Процедура ТипТекстаПриИзменении(Элемент)
	
	Если Объект.ТипТекста = "Любой текст" Тогда
		Объект.ТекстQRКода = "";	
	ИначеЕсли Объект.ТипТекста = "Ссылка" Тогда
		Объект.АдресСайта = "http://";
	ИначеЕсли Объект.ТипТекста = "Визитная карточка" Тогда
		Объект.Имя = "";
		Объект.Фамилия = "";
		Объект.Должность = "";
		Объект.Организация = "";
		Объект.Адрес = "";
		Объект.Город = "";
		Объект.Страна = "";
		Объект.НомерТелефона = "";
		Объект.НомерТелефонаРабочий = "";
		Объект.НомерТелефонаМобильный = "";
		Объект.НомерФакса = "";
		Объект.АдресПочты = "@";
		Объект.АдресСайта = "http://";
		Объект.ДеньРождения = Неопределено;  		
	ИначеЕсли Объект.ТипТекста = "Телефон" Тогда
		Объект.НомерТелефона = "";
	ИначеЕсли Объект.ТипТекста = "SMS" Тогда
		Объект.НомерТелефона = "";
		Объект.ТекстQRКода = "";
	ИначеЕсли Объект.ТипТекста = "MMS" Тогда
		Объект.НомерТелефона = "";
		Объект.ТекстQRКода = "";
	ИначеЕсли Объект.ТипТекста = "E-mail" Тогда
		Объект.АдресПочты = "@"; 
	ИначеЕсли Объект.ТипТекста = "E-mail с текстом" Тогда
		Объект.АдресПочты = "@"; 
		Объект.ТемаПисьма = ""; 
		Объект.ТекстQRКода = "";		
	ИначеЕсли Объект.ТипТекста = "Географическая информация" Тогда
		Объект.ТекстQRКода = ""; 
	КонецЕсли;
	
	ВидимостьГрупп(); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидимостьГрупп();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьГрупп()
	
	Элементы.ГруппаТекст.Видимость = Объект.ТипТекста = "Любой текст";
	Элементы.ГруппаСсылка.Видимость = Объект.ТипТекста = "Ссылка";
	Элементы.ГруппаВизитнаяКарточка.Видимость = Объект.ТипТекста = "Визитная карточка"; 
	Элементы.ГруппаТелефон.Видимость = Объект.ТипТекста = "Телефон"; 
	Элементы.ГруппаСМС.Видимость = Объект.ТипТекста = "SMS"; 
	Элементы.ГруппаММС.Видимость = Объект.ТипТекста = "MMS"; 
	Элементы.ГруппаПочта.Видимость = Объект.ТипТекста = "E-mail"; 
	Элементы.ГруппаПочтаСТекстом.Видимость = Объект.ТипТекста = "E-mail с текстом"; 
	Элементы.ГруппаГеографическаяИнформация.Видимость = Объект.ТипТекста = "Географическая информация"; 
	
КонецПроцедуры
